\
\
\  Testing Phase: Probability Discounting
\
\  Scott Ng-Evans 2013
\
\  For the laboratory of Susan Ferguson
\
\
\  From: Effects of lesions of the nucleus accumbens core on choice between
\        small certain rewards and large uncertain rewards in rats
\        Rudolf N Cardinal and Nathan J Howes, BMC Neuroscience 2005, 6:37.
\
\        and
\
\        Perturbations in different forms of cost/benefit decision
\        making induced by repeated amphetamine exposure
\        Stan B. Floresco & Jennifer M. Whelan, Psychopharmacology (2009) 205:189-201
\
\        Adapted from the Delayed Discounting program written in 2012 for Susan Ferguson's lab
\
\
\

\  Revisions:
\
\  6-2018: E. Crummy - "Could you modify our current
\          probability discounting task so that it has
\          five sessions instead of four, with the
\          ability in the variable menu window to change
\          the number of free and forced choice trials
\          per block?"
\          Added an additional block and uncommented menu
\          choice for number of blocks.
\          The ability to choose the number of forced and choice
\          trials per block is already in the menu.
\
\  6-2018: E. Crummy - "Could there be an option to run the program
\          so that, if a reward is omitted on the risky lever, that the
\          green cue light is turned on or to not have it a cue light
\          turned on? So, it would end up being changing the program so
\          that the white cue lights only be turned on if the rat presses
\          on the lever, and not when the levers are extended. When the
\          reward is omitted on the risky lever, there should be an option
\          in the variable of either having the green light turn on with,
\          or to have no cue associated with the 'punished' press."
\
\





\
\
\
\
\   OUTPUTS
\
   ^LeftLevOut     = 1
   ^RightLevOut    = 2
   ^Feeder         = 3
   ^LeftLt         = 4
   ^RightLt        = 5
   ^TrayLt         = 6
   ^HouseLt        = 7
   ^Tone           = 8
   ^Pump           = 9
   ^Fan            = 10
   ^GreenLight     = 11


\
\   INPUTS
\
   ^LeftLevIn      = 1
   ^RightLevIn     = 3
   ^HeadEntry      = 5
\
\



\ Z-PULSES

\ Z1  - Start trial
\ Z2  - Signal for reward delivery
\ Z3  - Reward delivery over
\ Z4  - Miss - Time for ITI - determined by timeout for no HE to extend levers
\         in time A(2) or no lever press in time A(2) once levers are
\         extended; or once reward is delivered, by houselight state set
\ Z5  - Lever extended, ready to accept input  *** NOT USED ***
\ Z6  - Forced or Choice FR complete, delay if any starts
\ Z7  - Choice made in choice trial
\ Z8  - Right lever extends
\ Z9  - Left lever extends
\ Z10 - Right lever cue light on
\ Z11 - Left lever cue light on
\ Z12 - Head entry to extend levers
\ Z13 - First head entry with pellet delivery
\ Z14 - Non-rewarded trial after response



\ VARIABLES

\ A(0)  = Active lever (1:Right 2:Left)
\ A(1)  = FR for active lever *** NOT USED IN FERGUSON VERSION ***
\ A(2)  = Time allowed before a miss (sec)
\ A(3)  = Maximum session time (min) *** NOT USED IN FERGUSON VERSION ***
\ A(4)  = Maximum number of reinforcers *** NOT USED IN FERGUSON VERSION ***
\ A(5)  = Maximum Intertrial Interval (sec) (miss-time or time to complete FR is subtracted)
\ A(6)  = Time-out time (sec) **** TIMEOUT NOT USED IN FERGUSON VERSION
\ A(7)  = FR for right lever
\ A(8)  = FR for left lever
\ A(9)  = Reward magnitude for right lever (number of pellets delivered)
\ A(10) = Reward magnitude for left lever (number of pellets delivered)
\ A(11) = Probability of reward for right lever - picked from B list
\ A(12) = Probability of reward for left lever - picked from D list
\ A(13) = Number of trial blocks to run
\ A(14) = Dummy variable for menu formatting
\ A(15) = Subtract delay from max ITI? (0:no 1:yes)  *** NOT USED IN FERGUSON VERSION ***
\ A(16) = Use lever cue lights(0:NO 1:YES) - use white lever cue light to signal when reward for lever press
\ A(17) = Indicates headentry for pellet retrieval has occurred
\ A(18) = Number of rewards on right lever
\ A(19) = Number of rewards on left lever
\ A(20) = Number of nonrewards on right lever
\ A(21) = Number of nonrewards on left lever
\ A(22) = Number of trials per block - if changed must change M list and menu
\ A(23) = Use green cue light (0:no 1:yes) - use green cue to signal when no reward for press
\ A(24) = Cue light duration for white light
\ A(25) = Cue light duration for green light
\ A(26) = A(24) converted to system time
\ A(27) = A(25) converted to system time
\
\ B()   = Probability list by block for right lever
\
\ C(0) = Temp variable for setting up lever pick list N
\ C(1) = Temp variable for holding last subscript of lever pick list
\ C(2) = Current ITI
\ C(3) = Trial counter
\ C(4) = Number of right lever presses
\ C(5) = Number of left lever presses
\ C(6) = Number of head entries into food hopper
\ C(7) = Number of misses for forced trials
\ C(8) = Number of misses for choice trials
\ C(9) = Current trial type (forced = 0, choice = 1)
\ C(10) = Counter for trial in block (0-11)
\ C(11) = **** NOT USED ****
\ C(15) = Number of choices on riskier lever (see display column 10)
\ C(16) = Number of choices on less-risky lever (see display column 11)
\ C(17) = Mean lever-press latency for right lever forced trials
\ C(18) = Mean lever-press latency for left lever forced trials
\ C(19) = Mean lever-press latency for right lever choice trials
\ C(20) = Mean lever-press latency for left lever choice trials
\ C(21) = Temporary variable for lever-press latency calculations
\ C(22-29) = Used in lever-press latency calculations
\ C(30) = Number of right lever ITI responses  *** NOT USED IN FERGUSON VERSION ***
\ C(31) = Number of left lever ITI responses   *** NOT USED IN FERGUSON VERSION ***
\ C(32) = Number of right lever delay responses  *** NOT USED IN FERGUSON VERSION ***
\ C(33) = Number of left lever delay responses *** NOT USED IN FERGUSON VERSION ***
\ C(34) = Number of right lever timeout responses
\ C(35) = Number of left lever timeout responses
\ C(36) = Used in block number calculation
\ C(37) = Current block number
\ C(38) = Number of incorrect presses on right lever
\ C(39) = Number of incorrect presses on left lever
\ C(40) = Number of right choices
\ C(41) = Number of left choices
\ C(42) = Stores trial start time for calculating ITI
\ C(43) = Stores time of pellet delivery to calculate head entry retrieval latency
\ C(44) = Number of misses due to no head entry initiation in A(2) seconds
\ C(45) = Time used to calculate initiating head entry latency
\ C(46) = Number of initiating head entries in current block
\ C(47) = Sum of head entry latencies in current block
\ C(48) = Number of pellet retrieval head entries
\ C(49) = Sum of pellet retieval head entry latencies
\ C(50) = Number of head entries during delay (right lever forced or choice)
\ C(51) = Number of head entries during delay (left lever forced or choice)
\ C(52) = Number of probabilistic rewards given in a block for right lever
\ C(53) = Number of probabilistic rewards given in a block for left lever
\ C(54) = Number of probabilistic rewards withheld in a block for right lever
\ C(55) = Number of probabilistic rewards withheld in a block for left lever
\
\ D()   = Probability list by block for left lever
\
\ E = Event stamps
\     1 - Right lever press
\     2 - Left lever press
\     3 - Right lever extends
\     4 - Left lever extends
\     5 - Pellet delivery
\     6 - Miss - no press in A(2) seconds
\     7 - Food hopper head entry
\     8 - House light turns on
\     9 - House light turns off
\    10 - Tone delivery
\    11 - Right lever retracts
\    12 - Left lever retracts
\    13 - Forced trial starts, ITI over
\    14 - Choice trials starts, ITI over
\    15 - Wrong choice in forced trial
\    16 - ITI starts
\    17 - Right cue light turns on
\    18 - Left cue light turns on
\    19 - Right cue light starts flashing  ****  NOT USED IN FERGUSON VERSION
\    20 - Left cue light starts flashing   ****  NOT USED IN FERGUSON VERSION
\    21 - Right cue light turns off
\    22 - Left cue lights turns off
\    23 - Timeout over  **** NOT USED IN FERGUSON VERSION
\    24 - Incorrect lever press in choice trial (can happen during FR completion)
\    25 - Tray light turns ON
\    26 - Tray light turns OFF
\    27 - Miss - no nosepoke to initiate trial in A(2) seconds
\    28 - Head entries during delay (right lever forced or choice)
\    29 - Head entries during delay (left lever forced or choice)
\    30 = Right lever delay response  *** NOT USED IN FERGUSON VERSION ***
\    31 = Left lever delay response *** NOT USED IN FERGUSON VERSION ***
\    32 = New block marker
\    33 = Green cue light on
\    34 = Green cue light off
\   100 - Session ends, data saved
\
\ F = Time stamps
\ G = Counter for event and time stamps
\ H = Session timer
\ I = Used to decide which lever list to use (J or K)
\ J = Pick list for levers for forced trials
\ K = Pick list for levers for forced trials
\ L = Even position in lever list to start from
\ M = List of trials for a block
\ N = Final container for lever pick list for forced trials
\ O = Mean head entry latencies to retrieve pellet per block (P(0) is block 1,..) for Right lever
\ P = Temporary variable for current reward (number of pellets to deliver).
\     Used to determine if reward is given.
\ Q = Initiating head entry latency mean per block (Q(0) is block 1,..)
\ R = Choices on right lever broken down by block (R(0) is block 1, R(1) is block 2, etc.)
\ S = Choices on left lever broken down by block (S(0) is block 1, S(1) is block 2, etc.)
\ T = Choices on riskier lever broken down by block (T(0) is block 1, T(1) is block 2, etc.)
\ U = Choices on less-risky lever broken down by block (U(0) is block 1, U(1) is block 2, etc.)
\ V = Mean head entry latencies to retrieve pellet per block (V(0) is block 1,..) for Left lever
\ X = Dummy variable for going through lever pick list N
\ Y = Number of pellets delivered
\ Z = Dummy variable for going through trial type list M

VAR_ALIAS FR for right lever = A(7)
VAR_ALIAS FR for left lever = A(8)
VAR_ALIAS Reward for right lever = A(9)
VAR_ALIAS Reward for left lever = A(10)
VAR_ALIAS Use lever cue lights(0:no 1:yes) = A(16)
VAR_ALIAS Duration of lev cue light (sec) = A(24)
VAR_ALIAS Use green cue light(0:no 1:yes) = A(23)
VAR_ALIAS Duration of green cue light (sec) = A(25)
VAR_ALIAS Time-limit to respond (sec) = A(2)
VAR_ALIAS Max Intertrial interval (40 sec min) = A(5) \ 40 seconds minimum

VAR_ALIAS Prob rt lev block 1 = B(0)
VAR_ALIAS Prob rt lev block 2 = B(1)
VAR_ALIAS Prob rt lev block 3 = B(2)
VAR_ALIAS Prob rt lev block 4 = B(3)
VAR_ALIAS Prob rt lev block 5 = B(4)

VAR_ALIAS Prob lt lev block 1 = D(0)
VAR_ALIAS Prob lt lev block 2 = D(1)
VAR_ALIAS Prob lt lev block 3 = D(2)
VAR_ALIAS Prob lt lev block 4 = D(3)
VAR_ALIAS Prob lt lev block 5 = D(4)

VAR_ALIAS Number of trial blocks to run(max 5) = A(13)
VAR_ALIAS Block trial types (0:forced 1:choice) = A(14)
VAR_ALIAS       Trial 1  : = M(0)
VAR_ALIAS       Trial 2  : = M(1)
VAR_ALIAS       Trial 3  : = M(2)
VAR_ALIAS       Trial 4  : = M(3)
VAR_ALIAS       Trial 5  : = M(4)
VAR_ALIAS       Trial 6  : = M(5)
VAR_ALIAS       Trial 7  : = M(6)
VAR_ALIAS       Trial 8  : = M(7)
VAR_ALIAS       Trial 9  : = M(8)
VAR_ALIAS       Trial 10 : = M(9)
VAR_ALIAS       Trial 11 : = M(10)
VAR_ALIAS       Trial 12 : = M(11)
VAR_ALIAS       Trial 13 : = M(12)
VAR_ALIAS       Trial 14 : = M(13)
VAR_ALIAS       Trial 15 : = M(14)
VAR_ALIAS       Trial 16 : = M(15)
VAR_ALIAS       Trial 17 : = M(16)
VAR_ALIAS       Trial 18 : = M(17)
VAR_ALIAS       Trial 19 : = M(18)
VAR_ALIAS       Trial 20 : = M(19)
VAR_ALIAS       Trial 21 : = M(20)
VAR_ALIAS       Trial 22 : = M(21)
VAR_ALIAS       Trial 23 : = M(22)
VAR_ALIAS       Trial 24 : = M(23)
VAR_ALIAS       Trial 25 : = M(24)
VAR_ALIAS       Trial 26 : = M(25)

\\VAR_ALIAS Sub trial time from max ITI (0:no 1:yes) = A(15)
\\VAR_ALIAS Timeout duration (sec) = A(6) not used in Ferguson version



DIM A = 100
DIM C = 100
DIM E = 20000
DIM F = 20000
DIM N = 11
DIM O = 50
DIM Q = 50
DIM R = 50
DIM S = 50
DIM T = 50
DIM U = 50
DIM V = 50

List B = 1, .5, .25, .125, .0625
List D = 1, 1, 1, 1, 1
List I = 1, 2
List J = 1, 2, 2, 1, 2, 1, 2, 1, 1, 2, 1, 2
List K = 2, 1, 2, 1, 1, 2, 1, 2, 1, 2, 2, 1
List L = 0, 2, 4, 6, 8, 10
List M = 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1





S.S.1,    \ Session setup, ITI timing, ITI lever presses
  S1,
    0.01": ON ^Fan;
           SET A(2)  = 10;     \  Time allowed before a miss (sec)
           SET A(5)  = 40;     \  Maximum Intertrial Interval (sec) (miss-time or time to complete FR is subtracted)
           SET A(6)  = 5;      \  Time-out time (sec) **** TIMEOUT NOT USED IN FERGUSON VERSION
           SET A(7)  = 1;      \  FR for right lever
           SET A(8)  = 1;      \  FR for left lever
           SET A(9)  = 4;      \  Reward magnitude for right lever (number of pellets delivered)
           SET A(10) = 1;      \  Reward magnitude for left lever (number of pellets delivered)
           SET A(11) = 0;
           SET A(12) = 0;
           SET A(13) = 4;      \ Number of trial blocks to run
           SET A(16) = 1;      \ Use lever cue lights(0:NO 1:YES) - use white lever cue light to signal when reward for lever press
           SET A(22) = 26;     \ Number of trials per block - if changed must change M list and menu
           SET A(23) = 1;      \ Use green cue light (0:no 1:yes) - use green cue to signal when no reward for press
           SET A(24) = 3;      \ Lever cue light duration (sec)
           SET A(25) = 3;      \ Green cue light duration (sec)
           SET C(1)  = 11;
           SET C(10) = 0;
           SET E(G) = -987.987, F(G) = -987.987;
           RANDI C(0) = I; RANDI X = L ---> S2
  S2,
    0.01": IF C(1)<0 [@ENDPICK, @CONTPICK]
           @ENDPICK: ---> S3
           @CONTPICK: IF C(0) = 1 [@CHJ, @CHK]
                      @CHJ: SET N(C(1)) = J(C(1)); SUB C(1) ---> S2
                      @CHK: SET N(C(1)) = K(C(1)); SUB C(1) ---> S2
  S3,
    #START: SET C(3) = 1, C(36) = 1, C(37) = 1, C(42) = H; LIST C(9) = M(Z);
            IF C(9) = 0 [@FORCED, @CHOICE]
            @FORCED: LIST A(0) = N(X); SET E(G) = 13, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S4
            @CHOICE: SET E(G) = 14, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S4
  S4,
    #Z4: SET C(2) = (A(5)*100) - (H - C(42)), E(G) = 16, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S5
  S5,
    C(2)#T: LIST C(9) = M(Z); SET A(17) = 0; IF C(9) = 0 [@FORCED, @CHOICE]
            @FORCED: LIST A(0) = N(X); Z1; ADD C(3), C(36); SET C(42) = H, E(G) = 13, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S4
            @CHOICE: Z1; ADD C(3), C(36); SET C(42) = H, E(G) = 14, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S4






S.S.2,   \ Levers extend, retract
  S1,
    #Z12: IF C(9) = 0 [@FORCED, @CHOICE]
          @FORCED: IF A(0) = 1 [@RIGHT, @LEFT]
                   @RIGHT: ON ^RightLevOut; Z8; SET E(G) = 3, F(G) = H;
                           SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S3
                   @LEFT:  ON ^LeftLevOut; Z9; SET E(G) = 4, F(G) = H;
                           SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S4
          @CHOICE: ON ^RightLevOut, ^LeftLevOut; Z8; Z9; SET E(G) = 3, F(G) = H;
                   SET E(G+1) = 4, F(G+1) = H, E(G+2) = -987.987, F(G+2) = -987.987; ADD G; ADD G ---> S2
  S2,
    #Z4!#Z6: IF A(0) = 1 [@RIGHT, @LEFT]    \\  TURN LEVER OFF ONLY ON OPPOSITE SIDE WITH CHOICE
         @RIGHT: OFF ^RightLevOut, ^LeftLevOut; SET E(G) = 11, F(G) = H;
                 SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
         @LEFT:  OFF ^LeftLevOut, ^RightLevOut; SET E(G) = 12, F(G) = H;
                 SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
    #Z7: IF A(0) = 2 [@RIGHT, @LEFT]    \\  TURN LEVER OFF ONLY ON OPPOSITE SIDE WITH CHOICE
         @RIGHT: OFF ^RightLevOut; SET E(G) = 11, F(G) = H;
                 SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> SX
         @LEFT:  OFF ^LeftLevOut; SET E(G) = 12, F(G) = H;
                 SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> SX
  S3,
    #Z4!#Z6: OFF ^RightLevOut; SET E(G) = 11, F(G) = H;
             SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
  S4,
    #Z4!#Z6: OFF ^LeftLevOut; SET E(G) = 12, F(G) = H;
             SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1






S.S.3,   \ Stimulus lights (lever cue lights)
   S1,
     #Z2: SET A(26) = A(24) * 1" ;
          IF A(16) = 1 [@CUE, @NOCUE]
          @CUE:  IF A(0) = 1 [@RIGHT, @LEFT]
                 @RIGHT: ON ^RightLt; OFF ^LeftLt; Z10;
                         SET E(G) = 17, F(G) = H;
                         SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
                 @LEFT:  ON ^LeftLt; OFF ^RightLt; Z11;
                         SET E(G) = 18, F(G) = H;
                         SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S3
          @NOCUE: ---> SX
  S2,
    A(26)#T: OFF ^RightLt, ^LeftLt;
             SET E(G) = 21, F(G) = H;
             SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
  S3,
    A(26)#T: OFF ^LeftLt, ^RightLt;
             SET E(G) = 22, F(G) = H;
             SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1






S.S.4,   \ Green cue light
   S1,
     #Z14: SET A(27) = A(25) * 1" ;
           IF A(23) = 1 [@CUE, @NOCUE]
           @CUE:  ON ^GreenLight;
                  SET E(G) = 33, F(G) = H;
                  SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
           @NOCUE: ---> SX
  S2,
    A(27)#T: OFF ^GreenLight;
             SET E(G) = 34, F(G) = H;
             SET E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1






S.S.5,   \ Houselight and ITI entry signal (Z4)
  S1,
    #START!#Z1: ON ^HouseLt; SET E(G) = 8, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
  S2,
    #Z4: OFF ^HouseLt; SET E(G) = 9, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
    #Z6: ---> S3
  S3,
    #Z2!#Z14: ---> S4
  S4,
    #Z13: ---> S5
    10": Z4; OFF ^HouseLt; SET E(G) = 9, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
  S5,
    6": Z4; OFF ^HouseLt; SET E(G) = 9, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1






S.S.6,   \ Traylight
  S1,
    #START!#Z1: ON ^TrayLt; SET E(G) = 25, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
  S2,
    #Z4: OFF ^TrayLt; SET E(G) = 26, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
    #Z12: OFF ^TrayLt; SET E(G) = 26, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S3
  S3,
    #Z2!#Z14: ON ^TrayLt; SET E(G) = 25, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S4
    #Z4: ---> S1
  S4,
    #Z13: OFF ^TrayLt; SET E(G) = 26, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1
    10": OFF ^TrayLt; SET E(G) = 26, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S1






S.S.7,   \ Pellet delivery
  S1,
    #Z2: IF A(0) = 1 [@RIGHT, @LEFT]
         @RIGHT: IF P=0 [@NOREWARD, @REWARD]
                 @NOREWARD: ---> S1
                 @REWARD: ON ^Feeder; ADD Y; SET E(G) = 5, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
         @LEFT: IF P=0 [@NOREWARD, @REWARD]
                 @NOREWARD: ---> S1
                 @REWARD: ON ^Feeder; ADD Y; SET E(G) = 5, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
  S2,
    0.1": OFF ^Feeder; SUB P; IF P=0 [@DONE, @NOTDONE]
          @DONE: ---> S4
          @NOTDONE: ---> S3
  S3,
    0.25": ON ^Feeder; ADD Y; SET E(G) = 5, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
  S4,
    0.25": Z3 ---> S1  \ allow some time for traylight if one pellet delivered






S.S.8,   \  Lever press fixed ratio/choice and probabilistic reward
  S1,
    #Z12: IF C(9) = 0 [@FORCED, @CHOICE]
          @FORCED: IF A(0) = 1 [@RIGHT, @LEFT]
                   @RIGHT: ---> S2
                   @LEFT:  ---> S4
          @CHOICE: ---> S6
  S2,
    A(2)*100#T: Z4; ADD C(7); SET E(G) = 6, F(G) = H, E(G+1) =-987.987, F(G+1) = -987.987; ADD G ---> S1
    1#R^RightLevIn: IF A(7) = 1 [@FRDONE, @FRNOTDONE]
                    @FRDONE: Z6 ---> S9
                    @FRNOTDONE: ---> S3
  S3,
    A(7)-1#R^RightLevIn: Z6 ---> S9
  S4,
    A(2)*100#T: Z4; ADD C(7); SET E(G) = 6, F(G) = H, E(G+1) =-987.987, F(G+1) = -987.987; ADD G ---> S1
    1#R^LeftLevIn: IF A(8) = 1 [@FRDONE, @FRNOTDONE]
                   @FRDONE: Z6 ---> S10
                   @FRNOTDONE: ---> S5
  S5,
    A(8)-1#R^LeftLevIn: Z6 ---> S10
  S6,
    1#R^RightLevIn: ADD C(40), R(C(37)-1); SET A(0) = 1; Z7;
                    IF A(11)<A(12) [@RISKYRESP, @LESSRISKYRESP]
                    @RISKYRESP: ADD C(15), T(C(37)-1); IF A(7) = 1 [@FRDONE, @FRNOTDONE]
                                @FRDONE: Z6 ---> S9
                                @FRNOTDONE: ---> S7
                    @LESSRISKYRESP: IF A(11)=A(12) [@EQUALRISK, @LESSRISKYRESP]
                                    @EQUALRISK: IF A(7) = 1 [@FRDONE, @FRNOTDONE]
                                                @FRDONE: Z6 ---> S9
                                                @FRNOTDONE: ---> S7
                                    @LESSRISKYRESP: ADD C(16), U(C(37)-1); IF A(7) = 1 [@FRDONE, @FRNOTDONE]
                                                    @FRDONE: Z6 ---> S9
                                                    @FRNOTDONE: ---> S7
    1#R^LeftLevIn: ADD C(41), S(C(37)-1); SET A(0) = 2; Z7;
                   IF A(12)<A(11) [@RISKYRESP, @LESSRISKYRESP]
                   @RISKYRESP: ADD C(15), T(C(37)-1); IF A(8) = 1 [@FRDONE, @FRNOTDONE]
                               @FRDONE: Z6 ---> S10
                               @FRNOTDONE: ---> S8
                   @LESSRISKYRESP: IF A(11)=A(12) [@EQUALRISK, @LESSRISKYRESP]
                                   @EQUALRISK: IF A(8) = 1 [@FRDONE, @FRNOTDONE]
                                               @FRDONE: Z6 ---> S10
                                               @FRNOTDONE: ---> S8
                                   @LESSRISKYRESP: ADD C(16), U(C(37)-1); IF A(8) = 1 [@FRDONE, @FRNOTDONE]
                                                   @FRDONE: Z6 ---> S10
                                                   @FRNOTDONE: ---> S8
    A(2)*100#T: Z4; ADD C(8); SET E(G) = 6, F(G) = H, E(G+1) =-987.987, F(G+1) = -987.987; ADD G ---> S1
  S7,
    A(7)-1#R^RightLevIn: Z6 ---> S9
  S8,
    A(8)-1#R^LeftLevIn: Z6 ---> S10
  S9,
    0": WITHPI = (A(11)*10000) [@REW, @NOREW]
        @REW: SET P = A(9); ADD A(18);
              IF ((A(18)/(A(18)+A(20))) > A(11)) AND (A(18)>1) [@NOREW, @REW]
              @NOREW: SUB A(18); ADD A(20); SET P = 0; Z14 ---> S1
              @REW: Z2 ---> S1
        @NOREW: SET P = 0; ADD A(20); Z14 ---> S1
  S10,
    0": WITHPI = (A(12)*10000) [@REW, @NOREW]
        @REW: SET P = A(10); ADD A(19);
              IF ((A(19)/(A(19)+A(21))) > A(12)) AND (A(19)>1) [@NOREW, @REW]
              @NOREW: SUB A(19); ADD A(21); SET P = 0; Z14 ---> S1
              @REW: Z2 ---> S1
        @NOREW: SET P = 0; ADD A(21); Z14 ---> S1






S.S.9,   \ Individual lever presses recorded and cumulated
  S1,
    #START: ---> S2
  S2,
    1#R^RightLevIn: ADD C(4); SET E(G) = 1, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> SX
    1#R^LeftLevIn: ADD C(5); SET E(G) = 2, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> SX






S.S.10,   \  Headentry recording and dependencies
  S1,
    #START: SET C(45) = H ---> S2
  S2,         \\\\\ head entry to extend levers
    1#R^HeadEntry: Z12; ADD C(6), C(46); SET E(G) = 7, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G;
                   SET C(47) = (H - C(45)) + C(47), Q(C(37)-1) = (C(47)/100)/C(46) ---> S3
    A(2)*100#T: Z4; ADD C(44); SET A(17) = 1, E(G) = 27, F(G) = H, E(G+1) =-987.987, F(G+1) = -987.987; ADD G ---> S5
  S3,          \\\\\ head entries before reward or miss
    1#R^HeadEntry: ADD C(6); SET E(G) = 7, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> SX
    #Z2!#Z14: SET C(43) = H ---> S4
    #Z4: SET A(17) = 1 ---> S5
  S4,          \\\\\ head entry to retrieve pellet
    1#R^HeadEntry: ADD C(6), A(17); SET E(G) = 7, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G;
                   IF A(17) = 1 [@FIRSTHE, @NOTFIRSTHE]
                   @FIRSTHE: Z13; ADD C(48); SET C(49) = (H - C(43)) + C(49);
                             IF A(0) = 1 [@RIGHT, @LEFT]
                             @RIGHT: SET O(C(37)-1) = (C(49)/100)/C(48) ---> SX
                             @LEFT:  SET V(C(37)-1) = (C(49)/100)/C(48) ---> SX
                   @NOTFIRSTHE: ---> SX
    10": ---> S5
    #Z4: ---> S5
  S5,          \\\\\ ITI headentries
    1#R^HeadEntry: ADD C(6); SET E(G) = 7, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G;
                   IF A(17) > 0 [@NOHESIGNAL, @HESIGNAL]
                   @NOHESIGNAL:   ---> SX
                   @HESIGNAL: Z13; ADD C(48); SET C(49) = (H - C(43)) + C(49);
                              IF A(0) = 1 [@RIGHT, @LEFT]
                              @RIGHT: SET O(C(37)-1) = (C(49)/100)/C(48) ---> SX
                              @LEFT:  SET V(C(37)-1) = (C(49)/100)/C(48) ---> SX
    #Z1: SET C(45) = H ---> S2






S.S.11,    \ Lever press latency from lever extends
  S1,
    #Z8!#Z9: SET C(21) = H ---> S2
  S2,
    1#R^RightLevIn: SET C(21) = H - C(21); IF C(9) = 0 [@FORCED, @CHOICE]
                    @FORCED: ADD C(22); SET C(24) = C(24) + C(21); SET C(17) = (C(24)/C(22))/100 ---> S1
                    @CHOICE: ADD C(26); SET C(27) = C(27) + C(21); SET C(19) = (C(27)/C(26))/100 ---> S1
    1#R^LeftLevIn: SET C(21) = H - C(21); IF C(9) = 0 [@FORCED, @CHOICE]
                   @FORCED: ADD C(23); SET C(25) = C(25) + C(21); SET C(18) = (C(25)/C(23))/100 ---> S1
                   @CHOICE: ADD C(28); SET C(29) = C(29) + C(21); SET C(20) = (C(29)/C(28))/100 ---> S1
    #Z4: ---> S1






S.S.12,    \ Calculation for current block number and block delay set, zero out block head entry latency calcs
  S1,
    #START: SET A(11) = B(0), A(12) = D(0);
            SET E(G) = 32, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> S2
  S2,
    #Z1: IF C(36)/A(22)<=1 [@NORESET, @RESET]
         @NORESET: ---> S2
         @RESET: SET C(48) = 0, C(49) = 0, C(46) = 0, C(47) = 0;
                 SET A(18) = 0, A(19) = 0, A(20) = 0, A(21) = 0;
                 SET C(36) = 1, A(11) = B(C(37)), A(12) = D(C(37)); ADD C(37);
                 SET E(G) = 32, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987; ADD G ---> SX






S.S.13,    \ Check that user entered reasonable max ITI - accounting for A(2) time limits and houselight time
  S1,
    #START: IF A(5) < 40 [@RESET, @ALLOW]
            @RESET: SET A(5) = 40 ---> SX
            @ALLOW: ---> SX






S.S.14,   \ Display
  S1,
    0.5": SHOW 1,  Trial: , C(3);
          SHOW 2,  Block: , C(37);
          SHOW 3,  Elapsed minutes: , H/6000;
          SHOW 6,  Misses (forced trials): , C(7);
          SHOW 7,  Misses (choice trials): , C(8);
          SHOW 8,  Misses (head entry): , C(44);
          SHOW 11,  Right Presses: , C(4);
          SHOW 12,  Left Presses: , C(5);
          SHOW 13,  Head Entries: , C(6);
          SHOW 16, Pellets:,  Y;
          SHOW 21, Riskier choices: , C(15);
          SHOW 22, Less-risky choices: , C(16);
          SHOW 26, Mean Rt lev lat FORCED: , C(17);
          SHOW 27, Mean Lt lev lat FORCED: , C(18);
          SHOW 28, Mean Rt lev lat CHOICE: , C(19);
          SHOW 29, Mean Lt lev lat CHOICE: , C(20);
          SHOW 31, Right Choices: , C(40);
          SHOW 32, Left Choices: , C(41) ---> S1






S.S.15,    \ Session timer
  S1,
    #START: ---> S2
  S2,
    0.01": ADD H; IF C(3)>(A(13) * A(22)) [@END, @NOTEND]
           @END: SET E(G) = 100, F(G) = H, E(G+1) = -987.987, F(G+1) = -987.987;
                 SET O(C(37)) = -987.987;
                 SET Q(C(37)) = -987.987;
                 SET V(C(37)) = -987.987;
                 SET R(C(37)) = -987.987;
                 SET S(C(37)) = -987.987;
                 SET T(C(37)) = -987.987;
                 SET U(C(37)) = -987.987;
                 SUB C(3);
                 SHOW 1,  Trial: , C(3);
                 SHOW 2,  Block: , C(37);
                 SHOW 3,  Elapsed minutes: , H/6000;
                 SHOW 6,  Misses (forced trials): , C(7);
                 SHOW 7,  Misses (choice trials): , C(8);
                 SHOW 8,  Misses (head entry): , C(44);
                 SHOW 11,  Right Presses: , C(4);
                 SHOW 12,  Left Presses: , C(5);
                 SHOW 13,  Head Entries: , C(6);
                 SHOW 16, Pellets:,  Y;
                 SHOW 21, Riskier choices: , C(15);
                 SHOW 22, Less-risky choices: , C(16);
                 SHOW 26, Mean Rt lev lat FORCED: , C(17);
                 SHOW 27, Mean Lt lev lat FORCED: , C(18);
                 SHOW 28, Mean Rt lev lat CHOICE: , C(19);
                 SHOW 29, Mean Lt lev lat CHOICE: , C(20);
                 SHOW 31, Right Choices: , C(40);
                 SHOW 32, Left Choices: , C(41) ---> STOPABORTFLUSH
           @NOTEND: ---> S2


