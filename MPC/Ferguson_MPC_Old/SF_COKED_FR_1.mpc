\
\ ************************
\ *                      *
\ *     SF_COKED_FR      *
\ *                      *
\ ************************ 
\ Scott Ng-Evans (2013)
\ Based on RP_COKED_FR (2009)
\ Methods modified from: 
\           Prefrontal synaptic markers of cocaine addiction-like behavior
\           in rats. F Kasanetz, M Lafourcade, V Deroche-Gamonet, J-M Revest, 
\           N Berson, E Balado, J-F Fiancette, P Renault, P-V Piazza and OJ Manzoni.
\           Molecular Psychiatry (2012), 1-9.
\ 
\
\                 Basic training protocol: The daily SA session was composed 
\                 of three drug components (40 min each) separated by 15 min 
\                 drug free periods. "Drug" periods were signaled by the blue 
\                 cue light, while the "no-drug" periods were signaled by 
\                 illumination of the entire SA box and extinction of the blue 
\                 cue light. During the "no-drug" periods, nose pokes were without 
\                 scheduled consequences. During the "drug" periods, introduction 
\                 of the animal's nose into one hole (active device) turned on the 
\                 white cue light located above it and then, 1 sec later, switched 
\                 on the infusion pump. The cue light remained on for a total of 4 
\                 sec. Nose pokes into the other hole (inactive device) had no 
\                 scheduled consequences. The self-infusion volume was 0.5 ?L (2 sec 
\                 infusion) and contained 2 µg/g of cocaine. Each infusion was followed 
\                 by a 40 sec time-out period. During the first 5 days, an FR1 schedule 
\                 of reinforcement (i.e. one nose poke resulted in an infusion of 2 µg/g 
\                 of cocaine) was applied. Then, the FR was first increased to 3 (1 to 2 
\                 sessions) and finally to 5 for the rest of the experiment. Criterion 
\                 for acquisition of cocaine SA was defined by a stable number of 
\                 self-infusions over at least three consecutive SA sessions (+/-10%). 

\
\
\ Revisions:
\
\
\           7 - 2009 - Added head entry detection as requested by Ben Whiddon
\
\
\
\

\
\
\ Input Constants

^LeftLeverIn  = 1
^RightLeverIn = 3
^HeadEntry    = 5


\ Output Constants (NOTE: Lever refers to extending or retracting)

^LeftLeverOut   = 1
^RightLeverOut  = 2
^Feeder         = 3
^LeftCueLight   = 4
^RightCueLight  = 5
^HouseLight     = 7
^Sonalert       = 8
^Syringepump    = 9
^Fan            = 10
^GreenLight     = 11
^Shock          = 12



\ Z-Pulses
\
\ Z1 time-out over drug available and drug period starts
\ Z2 drug delivery initiation
\ Z3 no-drug period starts



\ Variable Definitions:

\ A(0)  = Active press (0:right 1:left)
\ A(1)  = Number of drug periods
\ A(2)  = Length of drug periods (min)
\ A(3)  = Length of no-drug periods (min)
\ A(4)  = Drug infusion time (sec)
\ A(5)  = Post-infusion time-out length (sec)
\ A(6)  = Tally for number of drug periods
\ A(7)  = Switch for ending session when number of drug periods reached
\ A(8)  = Inter-infusion time limit; max time allowed between infusions before PR ends  **** NOT USED IN FR ***
\ A(9)  = Inter-infusion timer (1-second resolution)   **** NOT USED IN FR ****
\ A(10) = Active right presses
\ A(11) = Active left presses
\ A(12) = Inactive right presses
\ A(13) = Inactive left presses
\ A(14) = Variable for countdown of drug and no-drug period to display
\ \
\ A(16) = Food tray head entries
\
\
\ D = event time stamp
\ E = event identity stamp
\       3  - Right active FR press
\       4  - Left active FR press
\       5  - Infusion start
\       6  - Infusion end
\       7  - Food tray head entry
\	8  - Right inactive press during FR
\	9  - Left inactive press during FR
\      34  - Right press during infusion and time-out period
\      35  - Left press during infusion and time-out period
\      36  - Right press in no-drug period
\      37  - Left press in no-drug period
\      38  - Right lever cue light on
\      39  - Left lever cue light on
\      40  - Right lever cue light off
\      41  - Left lever cue light off
\      42  - Houselight on
\      43  - Houselight off
\     100  - End session marker
\      
\ F = FR countdown variable for right
\ G = Event counter
\ H = Session timer
\ U = FR countdown variable for left
\
\ X = fixed ratio
\ Y = number of reinforcers (infusions)


DIM A = 50
DIM D = 50000
DIM E = 50000


VAR_ALIAS Fixed Ratio = X
VAR_ALIAS Active lever (0:right 1:left) = A(0)
VAR_ALIAS Number of drug periods = A(1)
VAR_ALIAS Length of drug periods(min) = A(2)
VAR_ALIAS Length of no-drug periods(min) = A(3)
VAR_ALIAS Drug infusion time(sec) = A(4)
VAR_ALIAS Post-inf time-out length(sec) = A(5)



S.S.1,  \ Default set-up, drug/no-drug switching
  S1,  
      0": SET A(1) = 3, A(2) = 40, A(3) = 15, A(4) = 2, A(5) = 40, A(6) = 0, A(7) = 0;
          SET D(0) = -987.987, E(0) = -987.987, X = 1, Y = 0;
          ON ^Fan ---> S2
  S2, 
      #START: Z1; ADD A(6) ---> S3
  S3,
      A(2)*6000#T: IF S.S.4 = 1 [@SWITCH, @WAIT]
                   @SWITCH: IF A(6) >= A(1) [@END, @NOEND]
                            @END: SET A(7) = 1 ---> S6
                            @NOEND: Z3 ---> S4
                   @WAIT: ---> S5 \wait for post-infusion timeout to complete
  S4,
      A(3)*6000#T: Z1; ADD A(6) ---> S3
  S5,  \wait for post-infusion timeout to finish
     .01": IF S.S.4 = 1 [@SWITCH, @WAIT]
           @SWITCH: IF A(6) >= A(1) [@END, @NOEND]
                    @END: SET A(7) = 1 ---> S6
                    @NOEND: Z3 ---> S4
           @WAIT: ---> S5
  S6,
     1": ---> SX





S.S.2,  \ Individual active and inactive presses and FR
  S1,  
    #Z1: SET U = X, F = X; IF A(0) = 0 [@RIGHT, @LEFT]
         @RIGHT: ---> S3
         @LEFT:  ---> S2
  S2,
    1#R^LeftLeverIn: SET D(G) = H; SET E(G) = 4; ADD G, A(11); SET D(G) = -987.987; SET E(G) = -987.987; 
                     SUB U; IF U <= 0 [@FRDONE, @FRNOTDONE]
                                    @FRDONE: Z2 ---> S1
                                    @FRNOTDONE: ---> S2
    1#R^RightLeverIn: SET D(G) = H; SET E(G) = 8; ADD G, A(12); SET D(G) = -987.987; SET E(G) = -987.987 ---> S2
    #Z3: ---> S1
  S3,
    1#R^RightLeverIn: SET D(G) = H; SET E(G) = 3; ADD G, A(10); SET D(G) = -987.987; SET E(G) = -987.987; 
                      SUB F; IF F <= 0 [@FRDONE, @FRNOTDONE]
                                     @FRDONE: Z2 ---> S1
                                     @FRNOTDONE: ---> S3
    1#R^LeftLeverIn: SET D(G) = H; SET E(G) = 9; ADD G, A(13); SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
    #Z3: ---> S1





S.S.3,  \ Post reward, time-out and No-drug period presses
  S1,
    #Z2: ---> S2
    #Z3: ---> S3
  S2,
    1#R^RightLeverIn: SET D(G) = H; SET E(G) = 34; ADD G, A(12); SET D(G) = -987.987; SET E(G) = -987.987 ---> S2
    1#R^LeftLeverIn: SET D(G) = H; SET E(G) = 35; ADD G, A(13); SET D(G) = -987.987; SET E(G) = -987.987 ---> S2
    #Z1: ---> S1
    #Z3: ---> S3
  S3,     \ No-drug period
    1#R^RightLeverIn: SET D(G) = H; SET E(G) = 36; ADD G, A(12); SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
    1#R^LeftLeverIn: SET D(G) = H; SET E(G) = 37; ADD G, A(13); SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
    #Z1: ---> S1





S.S.4,  \ Infusion
  S1,
    #Z2: ---> S2
  S2,
    1": ON ^Syringepump; SET D(G) = H; SET E(G) = 5; ADD G, Y; SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
  S3,
    A(4)*100#T: OFF ^Syringepump; SET D(G) = H; SET E(G) = 6; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S4
  S4,
    A(5)*100#T: Z1 ---> S1





S.S.5,  \ Head entry detector
  S1,
    #START: ---> S2
  S2,
    1#R^HeadEntry: SET D(G) = H; SET E(G) = 7; ADD G, A(16); SET D(G) = -987.987; SET E(G) = -987.987 ---> SX





S.S.6,  \ House light
  S1,
    #Z1: OFF ^HouseLight; SET D(G) = H; SET E(G) = 43; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S2
  S2,
    #Z3: ON ^HouseLight; SET D(G) = H; SET E(G) = 42; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S1





S.S.7,   \ Lever Cue lights (in response to reward signal, ie. completing FR)
  S1,
    #Z1: ---> S2
  S2,
    #Z2: IF A(0) = 0 [@RIGHT, @LEFT]
         @RIGHT: ON ^RightCueLight; OFF ^LeftCueLight;
                 SET D(G) = H; SET E(G) = 38; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
         @LEFT:  ON ^LeftCueLight; OFF ^RightCueLight;
                 SET D(G) = H; SET E(G) = 39; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
  S3,
    4": IF A(0) = 0 [@RIGHT, @LEFT]
        @RIGHT: OFF ^RightCueLight; SET D(G) = H; SET E(G) = 40; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S1
        @LEFT:  OFF ^LeftCueLight; SET D(G) = H; SET E(G) = 41; ADD G; SET D(G) = -987.987; SET E(G) = -987.987  ---> S1
    #Z3: IF A(0) = 0 [@RIGHT, @LEFT]
         @RIGHT: OFF ^RightCueLight; SET D(G) = H; SET E(G) = 40; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S1
         @LEFT:  OFF ^LeftCueLight; SET D(G) = H; SET E(G) = 41; ADD G; SET D(G) = -987.987; SET E(G) = -987.987  ---> S1






S.S.8,   \ Levers extend
  S1,
    #Z1: ON ^RightLeverOut, ^LeftLeverOut ---> S1






S.S.9,  \ Display 
  S1,
    #START: ---> S2
  S2,
    .5": SHOW 1, INFUSIONS: , Y;
         SHOW 2, ACTIVE RIGHT: , A(10); 
         SHOW 3, ACTIVE LEFT: , A(11);
         SHOW 4, INACTIVE RIGHT: , A(12);
         SHOW 5, INACTIVE LEFT: , A(13);
         SHOW 6, FOOD TRAY HEAD ENT: , A(16) ---> S2





S.S.10,  \ Drug and no-drug period countdown
  S1,
    #START: SET A(14) = A(2)*60; SHOW 7, DRUG PERIOD SEC: , A(14) ---> S2
  S2,
    1": SUB A(14); IF A(14) < 0 [@NOMARK, @MARK]
        @NOMARK: ---> SX
        @MARK: SHOW 7, DRUG PERIOD SEC: , A(14) ---> SX
    #Z3: SET A(14) = A(3)*60; SHOW 7, NO-DRUG PERIOD SEC: , A(14) ---> S3
  S3,
    1": SUB A(14); IF A(14) < 0 [@NOMARK, @MARK]
        @NOMARK: ---> SX
        @MARK: SHOW 7, NO-DRUG PERIOD SEC: , A(14) ---> SX
    #Z1: SET A(14) = A(2)*60; SHOW 7, DRUG PERIOD SEC: , A(14) ---> S2

    



S.S.11,  \ Session timer
  S1,
    #START: ---> S2
  S2,
    0.01": ADD H; IF A(7) = 0 [@NOEND, @YESEND]
	   @NOEND: ---> SX
           @YESEND: SET D(G) = H, E(G) = 100; SET D(G+1) = -987.987, E(G+1) = -987.987 ---> STOPABORTFLUSH

