\
\ ******************************
\ *                            *
\ *   BASE: SF_TD_COKED_FR     *
\ *                            *
\ ******************************
\ Scott Ng-Evans (2016)
\
\
\ Tess Donckels: "Left Lever - Cue Light Self-Administration. Rats will be trained to press
\                 for IV infusions of saline, cocaine, or heroin (50uL delivered over 2.8 sec).
\                 The beginning of the session will be indicated by insertion of the left lever
\                 into the chamber. Each press of the left lever will result in an infusion and
\                 the illumination of the cue light above the lever for 20 seconds, during which
\                 time responses will not result in additional drug delivery (infusion should
\                 start when the cue light turns on). The infusions will be available on an FR1
\                 schedule. The end of the session will be indicated by retraction of the lever.
\                 No houselight will be illuminated during these sessions."
\
\
\
\ Revisions:
\
\  3-2017 - Susan Ferguson: "We wanted to start doing the within-session threshold procedure in
\                            our self-administration rats...
\                            The procedure is as outlined in the attached papers - we would use
\                            a cue light during the infusion period (no tone) and have no timeout
\                            period following the infusion period (active lever responses during
\                            the infusion period would be recorded but not have any consequences).
\                            We will be doing this in Tess’s polydrug use rats as well as standard
\                            rats, so it would be good to have the option of not having an inactive
\                            lever out."
\
\                            Later: "It turns out we have the “fancy” variable speed syringe pumps,
\                            which are manually set to deliver a specific volume/rate and this can’t
\                            be overridden by a program. So with these pumps we can’t do the within-
\                            session procedure - instead we will have to do the between-sessions method
\                            from the attached paper (2 hour sessions, each day a different infusion
\                            volume/rate that we manually set). Eventually I would like to get the
\                            standard pumps to do the within-session. So not sure if it is possible to
\                            have one program that would give options for either type of session - if
\                            not it makes sense to focus on the between-sessions method now."
\
\          Training phase from Oleson and Roberts, Neuropsychopharmacology, 2009 February; 34(3): 796-804:
\                           "The start of a session was signaled by the extension of a lever into the
\                            experimental chamber. During acquisition and during FR and PR training sessions
\                            cocaine was infused as a bolus of about 0.1 ml of cocaine solution over
\                            approximately 4-5 seconds (adjusted according to body weight). Doses
\                            (0.75 and 1.5 mg/kg/inf) were manipulated by adjusting the concentration of
\                            cocaine solution (2.5 and 5.0 mg/ml, respectively). Following each response
\                            the lever was retracted and a light was illuminated for a 20-second time-out period.
\
\                            During the acquisition phase all animals received access to cocaine (0.75 mg/kg/infusion)
\                            on an FR1 schedule of reinforcement during daily training sessions, which terminated after
\                            a maximum of 20 infusions or a period of 6 hours had elapsed."
\
\
\         Threshold phase from Oleson and Roberts, Neuropsychopharmacology, 2009 February; 34(3): 796–804:
\                           "During threshold testing the dose was manipulated by adjusting the pump duration as
\                            described below.
\                            Rats were tested through a descending series of unit injection doses reinforced on
\                            an FR1 reinforcement schedule during daily 2 hr sessions. Threshold was defined as
\                            the lowest dose that maintained daily intake. The dose was manipulated by holding
\                            the concentration of the drug constant and manipulating the pump duration. Pump
\                            times were decreased each day along a quarter-log scale as follows: 3156, 1780, 1000,
\                            562, 310, 178, 100, 56, 31, 18 and 10 msec. A time-out period corresponded only to
\                            the duration of the pump infusion."
\
\
\  3-2018 - for Alec Gisbson(JN Lab) - Adapted to within session dose decrementing procedure:
\             From Oleson, Richardson and Roberts. Psychopharmacology (Berl). 2011 March ; 214(2): 567–577.  :
\             "Following acquisition, animals were given access to cocaine in the within-session threshold
\             procedure. Pump times were decreased across 11 intervals according to a quarter-log scale
\             as follows: 3156, 1780, 1000, 562, 310, 178, 100, 56, 31, 18, and 10 ms. A timeout period
\             corresponded only to the duration of the pump infusion. The calculated dose equivalents (5
\             mg/ml X 1.6 ml/min X pump duration) are as follows: 421, 237, 133, 75, 41, 24, 13, 7.5, 4.1,
\             2.4, and 1.3µg/ infusion. See supplemental material of Oleson and Roberts (2009) for a full
\             characterization of this approach and validation that the appropriate quantity of drug is
\             delivered across all pump durations. The duration of the pump infusion was the only
\             experimenter-imposed inter-injection period. The available cocaine dose decreased every 10 min"
\
\           - Changed to InSessionDecDose
\
\
\  5-2018 - A. Gibson requested that tone be added. The choice to use tone was added to the menu (A(20)).
\
\  8-2018 - Added state set to capture all lever presses because some inactive ones could possibly be
\            missed in FR state set. Added codes 1 and 2 for generic right and left lever presses.
\
\  1-2019 - Changed doses based on Kawa, Bentzley and Robinson, Psychopharmacology (Berl). 2016 October;
\            233(19-20): 3587–3602.
\
\  1-2019 - Ferguson lab uses a 'PumpOff' signal since they have the pumps that have the infusion time
\            setting on a front dial; they therefore dial in a time longer than any infusion
\            would be and use the 'PumpOff' signal in conjuction with my pump controller box to
\            programmatically shut the pump off. I had to adjust the time that this 'PumpOff' signal
\            was on to at least 200 msec or it is not 'seen' by the pump. In this program it is set
\            to 300 msec to assure it will be read.
\
\  2-2019 - E. Crummy: "I was thinking of doing an experiment where I
\                       change the time of the footshock every ten minutes
\                       within a session. The footshock would be administered
\                       with an infusion of cocaine. It would be a similar
\                       design as the Kawa et al. and Benztley papers I sent
\                       you previously (can send again if you want), just with
\                       increasing the time on a tenth-log scale up to 2 seconds,
\                       if possible. The white cue light would be presented as
\                       normal with cocaine infusions, as well as a tone being
\                       paired for the duration of the shock."
\
\           E. Crummy emailed list of shock time - see S list below.
\
\           E. Crummy indicates she wants 2 shock-free periods at start of session by default.
\
\           - Added ability to turn off shock for the session (see menu, A(28) with -1 value).
\              This would make it a regular cocain decremental dose session.
\
\           - This is SF_InSessionIncremShockV1
\
\
\ 3-2019 - E. Crummy: "The program doesn't seem to end the session at the end of the final shock
\                       duration bin - is it programmed to last a certain amount of time or on
\                       some other criteria?
\
\                     Other add-ons:
\                     1. Can a green light be presented while the shock is ongoing?
\                     2. Can the dose be set to a pump duration of 2.8, instead of the initial dose of 8.1s?"
\
\           - This is SF_InSessionIncremShockV2
\
\
\



\ Input Constants
^LeftLeverIn  = 1
^RightLeverIn = 3
^HeadEntry    = 5


\ Output Constants (NOTE: Lever refers to extending or retracting)
^LeftLeverOut   = 1
^RightLeverOut  = 2
^Feeder         = 3
^LeftCueLight   = 4
^RightCueLight  = 5
^HouseLight     = 7
^Sonalert       = 8
^SyringePumpOn  = 9
^Fan            = 10
^GreenLight     = 11
^Shock          = 12
^SyringePumpOff = 14




\ Z-Pulses
\
\ Z1 infusion and cues over, drug available
\ Z2 drug delivery initiation
\ Z3 check required variable entry tally to start
\ Z4 start signal once information is entered
\ Z5 shock turns on
\ Z6 shock turns off


\ Variable Definitions:

\ A(0)  = Active lever (0:right 1:left)
\ A(1)  = Drug infusion time (sec) as set on pump   \ *** Within Session decrement follows list as in Olseon and Roberts
\ A(2)  = Post-infusion time-out length (sec)       \ *** Not used in threshold phase
\ A(3)  = Max session length in hours               \ *** Not used in threshold phase
\ A(4)  = Fixed ratio - FR
\ A(5)  = Session time displayed in minutes
\ A(6)  = Dummy variable for Wizard first-line bug
\ A(7)  = Dummy variabe for menu display
\ A(8)  = Maximum number of drug infusions to end session
\ A(9)  = Houselight during the session (0:off 1:on)
\ A(10) = Extend both levers (0:Both 1:only active)
\ A(11) = Use white cue lights or green light (0:White 1:Green)
\ A(12) = Dose per infusion (mg cocaine/kg)          \ *** for within session, dose decrements within infusion
\ A(13) = Animal weight (kg)
\ A(14) = How often to change dose (min)
\ A(15) = A(14) converted to system time
\ A(16) = Subscript for D list
\ A(17) = Current infusion time from D list in seconds (determines dose)
\ A(18) = A(17) in system time
\ A(19) = Current number of dose decrements
\ A(20) = Use tone during infusion? (0:no 1:yes)
\ A(21) = Number of head entries
\ A(22) = Decrement dose (0:No 1:Yes)
\ A(23) = Increment shock duration (0:No 1:Yes)
\ A(24) = Current number of shock duration increments
\ A(25) = Time between shock duration changes (min)
\ A(26) = Dummy variable for menu
\ A(27) = A(25) converted to system time
\ A(28) = Number of shock-free periods at the start
\ A(29) = Subscript for S list
\ A(30) = Current shock duration from S list in seconds
\ A(31) = A(30) in system time
\ A(32) = Number of shock free periods from A(28) used for subtraction
\ A(33) = Count of shocks
\
\ B(0)  = Active FR right presses
\ B(1)  = Active FR left presses
\ B(2)  = Right presses during pump activation
\ B(3)  = Left presses during pump activation
\
\ C(0) = Calculated timeout time (sec) - same as infusion time in threshold phase
\ C(1) = Calculated infusion time (sec)                 **** For within session decrement this is A(18)
\ C(2) = Cumulation to check if all required variables are entered
\
\ D()  = List of infusion times in seconds
\
\ E = event identity stamp
\       1  - Right lever press
\       2  - Left lever press
\       3  - Right active FR press
\       4  - Left active FR press
\       5  - Infusion start
\       6  - Infusion end
\       7  - Food tray head entry
\      10  - Right lever extends
\      11  - Left lever extends
\      12  - Right lever retracts
\      13  - Left lever retracts
\      36  - Right press during infusion
\      37  - Left press during infusion
\      38  - Right lever cue light on
\      39  - Left lever cue light on
\      40  - Right lever cue light off
\      41  - Left lever cue light off
\      42  - Houselight on
\      43  - Houselight off
\      44  - Start of timeout period
\      45  - End of timeout period
\      50  - Green cue light on
\      51  - Green cue light off
\      52  - Decrement dose
\      53  - Tone on
\      54  - Tone off
\      55  - Increment shock duration
\      56  - Shock on
\      57  - Shock off
\     100  - End session marker
\
\ G = Event counter
\ H = Session timer
\
\ I = List doses (ug/infusion)
\
\ L = FR countdown variable for left
\ R = FR countdown variable for right
\ S = Shock times (sec)
\ T = Event time stamp
\ Y = number of reinforcers (infusions)


LIST D = 2.8, 2.8, 2.8, 2.8, 2.8, 2.8, 2.8, 2.8, 2.8, 2.8, 2.8
         \\ 8.175, 4.597, 2.585, 1.454, .818, .460, .259, .145, .082, .046, .026  - Old list for decrementing dose
LIST I = 122, 122, 122, 122, 122, 122, 122, 122, 122, 122, 122
         \\ 383.5, 215.6, 121.3, 68.2, 38.3, 21.6, 12.1, 6.8, 3.8, 2.2, 1.2
LIST S = 0.2, 0.25, 0.32, 0.4, 0.5, 0.63, 0.8, 1, 1.26, 1.59, 2


DIM A = 50
DIM B = 10
DIM C = 10
DIM E = 50000
DIM T = 50000




VAR_ALIAS ------------------------------------------------ = A(26)
VAR_ALIAS Fixed Ratio = A(4)
VAR_ALIAS Active lever (0:right 1:left) = A(0)
VAR_ALIAS Extend both levers (0:Both 1:only active) = A(10)
VAR_ALIAS Houselight during session (0:OFF 1:ON) = A(9)
VAR_ALIAS Use tone during infusion (0:no 1:yes) = A(20)
VAR_ALIAS Animal weight (kg) = A(13)
VAR_ALIAS ----------------------------------------------- = A(26)
VAR_ALIAS Decrement dose (0:No 1:Yes) = A(22)
VAR_ALIAS Time between dose changes (min) = A(14)
VAR_ALIAS ------------- INFUSION DURATIONS(SEC) -------------- = A(26)
VAR_ALIAS Duration 1  = D(0)
VAR_ALIAS Duration 2  = D(1)
VAR_ALIAS Duration 3  = D(2)
VAR_ALIAS Duration 4  = D(3)
VAR_ALIAS Duration 5  = D(4)
VAR_ALIAS Duration 6  = D(5)
VAR_ALIAS Duration 7  = D(6)
VAR_ALIAS Duration 8  = D(7)
VAR_ALIAS Duration 9  = D(8)
VAR_ALIAS Duration 10 = D(9)
VAR_ALIAS Duration 11 = D(10)
VAR_ALIAS ----------------- DOSES(ug/infusion) ---------------- = A(26)
VAR_ALIAS Dose 1   = I(0)
VAR_ALIAS Dose 2   = I(1)
VAR_ALIAS Dose 3   = I(2)
VAR_ALIAS Dose 4   = I(3)
VAR_ALIAS Dose 5   = I(4)
VAR_ALIAS Dose 6   = I(5)
VAR_ALIAS Dose 7   = I(6)
VAR_ALIAS Dose 8   = I(7)
VAR_ALIAS Dose 9   = I(8)
VAR_ALIAS Dose 10  = I(9)
VAR_ALIAS Dose 11  = I(10)
VAR_ALIAS -------------------------------------------------------------------- = A(26)
VAR_ALIAS Increment shock duration (0:No 1:Yes) = A(23)
VAR_ALIAS Time between shock duration changes (min) = A(25)
VAR_ALIAS Shock-free periods at start (-1: entire session shock-free) = A(28)
VAR_ALIAS ------------------ SHOCK TIMES (sec) -------------- = A(26)
VAR_ALIAS Shock duration 1   = S(0)
VAR_ALIAS Shock duration 2   = S(1)
VAR_ALIAS Shock duration 3   = S(2)
VAR_ALIAS Shock duration 4   = S(3)
VAR_ALIAS Shock duration 5   = S(4)
VAR_ALIAS Shock duration 6   = S(5)
VAR_ALIAS Shock duration 7   = S(6)
VAR_ALIAS Shock duration 8   = S(7)
VAR_ALIAS Shock duration 9   = S(8)
VAR_ALIAS Shock duration 10  = S(9)
VAR_ALIAS Shock duration 11  = S(10)
VAR_ALIAS ------------------------------------------------------------------ = A(26)




\\  These are not used in the Threshold phase of the within-session procedure
\\VAR_ALIAS Max session length (hour) = A(3)
\\VAR_ALIAS Drug infusion time on pump (sec) = A(1)  \\ **** Must be entered as set on pump *** *** Within Session decrement follows list as in Olseon and Roberts
\\VAR_ALIAS Dose per infusion (mg/kg) = A(12)
\\VAR_ALIAS Max number of drug infusions = A(8)
\\VAR_ALIAS Time-out length (sec) = A(2)   \\ **** NO timeout for threshold phase  - only the time the pump is on
\\\VAR_ALIAS Use white cue lights or green light (0:White 1:Green) = A(11)








\\S.S.1,  \ Check that pump duration was entered
\\  S1,
\\    #START: IF A(1) = 0 [@HALT, @GO]
\\            @HALT: SHOW 1, ENTER TIME ON PUMP!! , A(1) ---> S2
\\            @GO: CLEAR 1,200; Z3; ADD C(2) ---> SX
\\  S2,
\\    0.1": IF A(1) = 0 [@HALT, @GO]
\\           @HALT: SHOW 1, ENTER TIME ON PUMP!! , A(1) ---> S2
\\           @GO: CLEAR 1,200; Z3; ADD C(2) ---> S1




\\S.S.2,  \ Check that dose per infusion was entered
\\  S1,
\\    #START: IF A(12) = 0 [@HALT, @GO]
\\            @HALT: SHOW 1, ENTER DOSE PER INFUSION!! , A(12) ---> S2
\\            @GO: CLEAR 1,200; Z3; ADD C(2) ---> SX
\\  S2,
\\    0.1": IF A(12) = 0 [@HALT, @GO]
\\          @HALT: SHOW 1, ENTER DOSE PER INFUSION!! , A(12) ---> S2
\\          @GO: CLEAR 1,200; Z3; ADD C(2) ---> S1




S.S.3,  \ Check that animal weight was entered
  S1,
    #START: IF A(13) = 0 [@HALT, @GO]
            @HALT: SHOW 1, ENTER ANIMAL WEIGHT!! , A(13) ---> S2
            @GO: CLEAR 1,200; Z3; ADD C(2) ---> SX
  S2,
    0.1": IF A(13) = 0 [@HALT, @GO]
          @HALT: SHOW 1, ENTER ANIMAL WEIGHT!! , A(13) ---> S2
          @GO: CLEAR 1,200; Z3; ADD C(2) ---> S1





S.S.4,  \ Check to see if all variables entered
  S1,
    #Z3: IF C(2) >= 1 [@GO, @WAIT]
         @GO: Z4 ---> SX
         @WAIT: ---> SX





S.S.5,  \ Default set-up and time-out timing
  S1,
    0": SET A(0)  = 0;         \Active lever (0:right 1:left)
        SET A(2)  = 0;         \Time-out length (sec)      **** No timeout in threshold phase ****
        SET A(3)  = 2;         \Maximum session length in hours
        SET A(4)  = 1;         \Fixed ratio - FR
        SET A(9)  = 0;         \Houselight during the session (0:OFF 1:ON)
        SET A(10) = 0;         \Extend both levers (0:Both 1:only active)
        SET A(11) = 0;         \Use white cue lights or green light (0:White 1:Green)
        SET A(13) = 0;         \Animal weight in kg
        SET A(14) = 10;        \Time between dose decrements in minutes
        SET A(20) = 0;         \Use tone during time-out (0:no 1:yes)
        SET A(22) = 0;         \Decrement dose (0:No 1:Yes)
        SET A(23) = 1;         \Increment shock duration (0:No 1:Yes)
        SET A(25) = 10;        \Time between shock duration changes (min)
        SET A(28) = 2;         \Number of shock-free periods at start

        \\\\\   NOT USED IN THRESHOLD PHASE OF WITHIN-SESSION PROCEDURE
        SET A(1)  = 0;         \Drug infusion time (sec)   **** Must be entered as set on pump ****
        SET A(8)  = 20;        \Maximum number of drug infusions to end session
        SET A(12) = 0;         \Dose per infusion (mg/kg)
        \\\\\

        SET T(0)  = -987.987; SET E(0) = -987.987;
        ON ^Fan ---> S2
  S2,
    #Z4: ---> S3
  S3,
    #Z2: SET T(G) = H, E(G) = 44; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> S4
  S4,
    #Z1: SET T(G) = H, E(G) = 45; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> S3





S.S.6,  \ Decrement dose
  S1,
    #Z4: IF A(22) = 0 [@NODECDOSE, @DECDOSE]
         @NODECDOSE: SET A(17) = D(0), A(18) = A(17) * 1" ---> SX
         @DECDOSE: ADD A(19);
                   IF A(19) < 12 [@CONTINUE, @SESSIONENDS]
                   @CONTINUE: SET A(15) = A(14) * 1';
                              LIST A(17) = D(A(16)); SET A(18) = A(17) * 1" ---> S2
                   @SESSIONENDS: ---> S1
  S2,
    A(15)#T: ADD A(19);
             IF A(19) < 12 [@CONTINUE, @SESSIONENDS]
             @CONTINUE: SET T(G) = H, E(G) = 52; ADD G; SET T(G) = -987.987, E(G) = -987.987;
                        LIST A(17) = D(A(16)); SET A(18) = A(17) * 1" ---> S2
             @SESSIONENDS: ---> S1





S.S.7,  \ Increment shock duration
  S1,
    #Z4: SET A(27) = A(25) * 1';
         IF A(23) = 0 [@NOINCDUR, @INCDUR]
         @NOINCDUR: IF A(28) < 0 [@NOSHOCK, @SHOCK]
                    @NOSHOCK: SET A(31) = 0 ---> SX
                    @SHOCK: SET A(30) = S(0); SET A(31) = A(30) * 1" ---> SX
         @INCDUR: IF A(28) < 0 [@NOSHOCK, @SHOCK]
                  @NOSHOCK: SET A(31) = 0 ---> SX
                  @SHOCK: SET A(32) = A(28);
                          SUB A(32);
                          IF A(32) >= 0 [@NOSHOCKYET, @SHOCKNOW]
                          @NOSHOCKYET: SET A(31) = 0 ---> S2
                          @SHOCKNOW: ADD A(24);
                                     IF A(24) < 12 [@CONTINUE, @SESSIONENDS]
                                     @CONTINUE: LIST A(30) = S(A(29)); SET A(31) = A(30) * 1" ---> S2
                                     @SESSIONENDS: ---> S1
  S2,
    A(27)#T: SUB A(32);
             IF A(32) >= 0 [@NOSHOCKYET, @SHOCKNOW]
             @NOSHOCKYET: SET A(31) = 0 ---> S2
             @SHOCKNOW: ADD A(24);
                        IF A(24) < 12 [@CONTINUE, @SESSIONENDS]
                        @CONTINUE: SET T(G) = H, E(G) = 55; ADD G; SET T(G) = -987.987, E(G) = -987.987;
                                   LIST A(30) = S(A(29)); SET A(31) = A(30) * 1" ---> S2
                        @SESSIONENDS: ---> S1





S.S.8,  \ Individual active presses and FR
  S1,
    #Z4!#Z1: IF A(0) = 0 [@RIGHT, @LEFT]
             @RIGHT: SET R = A(4) ---> S3
             @LEFT: SET L = A(4) ---> S2
  S2,
    1#R^LeftLeverIn: SET T(G) = H, E(G) = 4; ADD G, B(1); SET T(G) = -987.987, E(G) = -987.987;
                     SUB L; IF L <= 0 [@FRDONE, @FRNOTDONE]
                            @FRDONE: Z2 ---> S1
                            @FRNOTDONE: ---> S2
  S3,
    1#R^RightLeverIn: SET T(G) = H, E(G) = 3; ADD G, B(0); SET T(G) = -987.987, E(G) = -987.987;
                      SUB R; IF R <= 0 [@FRDONE, @FRNOTDONE]
                             @FRDONE: Z2 ---> S1
                             @FRNOTDONE: ---> S3





S.S.9,  \ Capture all lever presses
  S1,
    #Z4: ---> S2
  S2,
    1#R^RightLeverIn: SET T(G) = H, E(G) = 1; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
    1#R^LeftLeverIn: SET T(G) = H, E(G) = 2; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX





S.S.10,  \ Responses during infusion
  S1,
    #Z2: ---> S2
  S2,
    #Z1: ---> S1
    1#R^RightLeverIn: SET T(G) = H, E(G) = 36; ADD G, B(2); SET T(G) = -987.987, E(G) = -987.987 ---> SX
    1#R^LeftLeverIn: SET T(G) = H, E(G) = 37; ADD G, B(3); SET T(G) = -987.987, E(G) = -987.987 ---> SX





S.S.11,  \ Head entry detector
  S1,
    #Z4: ---> S2
  S2,
    1#R^HeadEntry: SET T(G) = H; SET E(G) = 7; ADD G, A(21); SET T(G) = -987.987; SET E(G) = -987.987 ---> SX





S.S.12,  \ Infusion - the SyringePumpOff signal makes use of control box on pumps with dial-in
         \             timing and speed controls - it shuts the pump down independent of those
         \             settings
  S1,
    #Z2: ON ^SyringePumpOn; SET T(G) = H, E(G) = 5; ADD G, Y; SET T(G) = -987.987, E(G) = -987.987 ---> S2
  S2,
    A(18)#T: OFF ^SyringePumpOn; ON ^SyringePumpOff; SET T(G) = H, E(G) = 6; ADD G;
             SET T(G) = -987.987, E(G) = -987.987 ---> S3
  S3,
    0.3": Z1; OFF ^SyringePumpOff ---> S1





S.S.13,  \ Shock
  S1,
    #Z2: IF A(31) = 0 [@NOSHOCK, @SHOCK]
         @NOSHOCK: ---> SX
         @SHOCK: ON ^Shock; Z5; ADD A(33); SET T(G) = H, E(G) = 56; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> S2
  S2,
    A(31)#T: OFF ^Shock; Z6; SET T(G) = H, E(G) = 57; ADD G;
             SET T(G) = -987.987, E(G) = -987.987 ---> S1





S.S.14,  \ Tone
  S1,
    #Z4: IF A(28) < 0 [@NOSHOCK, @SHOCK]
         @NOSHOCK: ---> S2
         @SHOCK: ---> S3
  S2,
    #Z2: IF A(20) = 0 [@NOTONE, @TONE]
         @NOTONE: ---> SX
         @TONE: ON ^Sonalert; SET T(G) = H, E(G) = 53; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
    #Z1: IF A(20) = 0 [@NOTONE, @TONE]
         @NOTONE: ---> SX
         @TONE: OFF ^Sonalert; SET T(G) = H, E(G) = 54; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
  S3,
    #Z5: IF A(20) = 0 [@NOTONE, @TONE]
         @NOTONE: ---> SX
         @TONE: ON ^Sonalert; SET T(G) = H, E(G) = 53; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
    #Z6: IF A(20) = 0 [@NOTONE, @TONE]
         @NOTONE: ---> SX
         @TONE: OFF ^Sonalert; SET T(G) = H, E(G) = 54; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX





S.S.15,  \ House light
  S1,
    #Z4: IF A(9) = 0 [@HLOFF, @HLON]
         @HLOFF: ---> SX
         @HLON: ON ^HouseLight; SET T(G) = H, E(G) = 42; ADD G; SET T(G) = -987.987; SET E(G) = -987.987 ---> SX





S.S.16,   \ Lever Cue lights
  S1,
    #Z2: IF A(0) = 0 [@RIGHT, @LEFT]
         @RIGHT: IF A(11) > 0 [@GREEN, @WHITE]
                 @GREEN: ON ^GreenLight;
                         SET T(G) = H, E(G) = 50; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
                 @WHITE: ON ^RightCueLight; OFF ^LeftCueLight;
                         SET T(G) = H, E(G) = 38; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
         @LEFT:  IF A(11) > 0 [@GREEN, @WHITE]
                 @GREEN: ON ^GreenLight;
                         SET T(G) = H, E(G) = 50; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
                 @WHITE: ON ^LeftCueLight; OFF ^RightCueLight;
                         SET T(G) = H; SET E(G) = 39; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
    #Z1: IF A(0) = 0 [@RIGHT, @LEFT]
         @RIGHT: IF A(11) > 0 [@GREEN, @WHITE]
                 @GREEN: OFF ^GreenLight;
                         SET T(G) = H, E(G) = 51; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
                 @WHITE: OFF ^RightCueLight, ^LeftCueLight;
                         SET T(G) = H, E(G) = 40; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
         @LEFT:  IF A(11) > 0 [@GREEN, @WHITE]
                 @GREEN: OFF ^GreenLight;
                         SET T(G) = H, E(G) = 51; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
                 @WHITE: OFF ^LeftCueLight, ^RightCueLight;
                         SET T(G) = H; SET E(G) = 41; ADD G; SET T(G) = -987.987, E(G) = -987.987  ---> SX





S.S.17,   \ Green Cue light
  S1,
    #Z5: ON ^GreenLight;
         SET T(G) = H, E(G) = 50; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
    #Z6: OFF ^GreenLight;
         SET T(G) = H, E(G) = 51; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX





S.S.18,   \ Levers extend
  S1,
    #Z4: IF A(10) > 0 [@ONELEVER, @TWOLEVERS]
         @ONELEVER: IF A(0) = 0 [@RIGHT, @LEFT]
                    @RIGHT: ON ^RightLeverOut; SET T(G) = H, E(G) = 10; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
                    @LEFT: ON ^LeftLeverOut; SET T(G) = H, E(G) = 11; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX
         @TWOLEVERS: ON ^RightLeverOut, ^LeftLeverOut;
                     SET T(G) = H, E(G) = 10; ADD G; SET T(G) = H, E(G) = 11; ADD G; SET T(G) = -987.987, E(G) = -987.987 ---> SX





S.S.19,  \ Session time display
  S1,
    #Z4: SHOW 2, SESS TIME(MIN): , A(5)/60 ---> S2
  S2,
    1": ADD A(5); SHOW 2, SESS TIME(MIN): , A(5)/60 ---> S2





S.S.20,  \ Display
  S1,
    #Z4: ---> S2
  S2,
    .2": SHOW 1,  INFUSION TIME(sec): , A(17);
         SHOW 3,  INFUSIONS: , Y;
         SHOW 6,  ACTIVE RT: , B(0);
         SHOW 7,  ACTIVE LT: , B(1);
         SHOW 8,  ANIMAL WEIGHT(kg) : , A(13);
         SHOW 11, RT DURING INFUSION : , B(2);
         SHOW 12, LT DURING INFUSION : , B(3);
         SHOW 16, SHOCKS: , A(33);
         SHOW 17, SHOCK DUR(SEC): , A(30) ---> S2





S.S.21,  \ Session timer and termination
  S1,
    #Z4: ---> S2
  S2,
    0.01": ADD H;
           IF (A(19) < 12) AND (A(24) < 12)  [@NOEND, @YESEND]
           @NOEND: ---> S2
           @YESEND: SET T(G) = H, E(G) = 100; SET T(G+1) = -987.987, E(G+1) = -987.987;
                    SHOW 1,  INFUSION TIME(sec): , A(17);
                    SHOW 3,  INFUSIONS: , Y;
                    SHOW 6,  ACTIVE RT: , B(0);
                    SHOW 7,  ACTIVE LT: , B(1);
                    SHOW 8,  ANIMAL WEIGHT(kg) : , A(13);
                    SHOW 11, RT DURING INFUSION : , B(2);
                    SHOW 12, LT DURING INFUSION : , B(3);
                    SHOW 16, SHOCKS: , A(33);
                    SHOW 17, SHOCK DUR(SEC): , A(30) ---> STOPABORTFLUSH

