\
\ ************************************
\ *                                  *
\ *      OFC_RI_VI_FreePellets       *
\ *                                  *
\ ************************************
\ Scott Ng-Evans (2013)
\ Based on RP_COKED_FR (2009)
\ Methods modified from:
\
\    Nat Commun. 2013 ; 4: 2264. Orbitofrontal and striatal circuits dynamically
\    encode the shift between goal-directed and habitual actions.
\    Christina M. Gremel1 and Rui M. Costa
\
\
\       Each training session commenced with illumination of the house light
\       and lever extension, and ended following schedule completion or after
\       90 min with the lever retracting and the house-light turning off.
\
\       On the first day, mice were trained to approach the food magazine
\       (no lever present) in each context on a random time (RT) schedule,
\       with a reinforcer delivered on average every 60 sec for a total of 15 min.
\
\

\
\
\ Revisions:
\
\
\
\
\
\
\

\
\
\ Input Constants

^LeftLeverIn  = 1
^RightLeverIn = 3
^HeadEntry    = 5


\ Output Constants (NOTE: Lever refers to extending or retracting)

^LeftLeverOut   = 1
^RightLeverOut  = 2
^Feeder         = 3
^LeftCueLight   = 4
^RightCueLight  = 5
^HouseLight     = 7
^Sonalert       = 8
^Syringepump    = 9
^Fan            = 10
^GreenLight     = 11
^Shock          = 12



\Z-PULSES

\Z1 - Start interval
\Z2 - Interval elapsed, time for reward
\Z3 - Reward delivery complete



\VARIABLES

\ A(0)  = Session length (min)
\ A(1)  = Mean random interval (sec)
\ A(2)  = Current interval time
\ A(3)  = Subscript for L list
\ A(4)  = Subscript for N list
\ A(5)  = Subscript for R list
\ A(6)  = Subscript for O list
\ A(7)  = Session time count up in minutes
\ A(8)  = VI countdown
\ A(9)  = Interval count
\
\ B(0)  = A(0) converted to system time
\ B(1)  = A(1) converted to system time
\ B(2)  = A(2) converted to system time
\
\ E()   = Event stamp array
\          1 - Press right lever
\          2 - Press left lever
\          3 - Head entry into food tray
\          4 - Right lever cue light on
\          5 - Right lever cue light off
\          6 - Left lever cue light on
\          7 - Left lever cue light off
\          8 - House light on
\          9 - House light off
\         10 - Pellet delivery
\         11 - Right lever extends
\         12 - Right lever retracts
\         13 - Left lever extends
\         14 - Left lever retracts
\         15 - Variable interval starts
\         16 - Variable interval ends
\        100 - Session end
\
\ G    = Subscript for event and time stamp arrays
\ H    = Session timer (10 msec resolution)
\ I    = Interval before rounding
\ J    = Interval after rounding
\ L()  = Times of left presses (sec)
\ N()  = Times of head entries (sec)
\ O()  = Times of pellet delivery (sec)
\ R()  = Times of right presses (sec)
\ T()  = Time stamp array
\ V()  = VI times



List V = 1,2,3,4,5,6,7,8,9,10



DIM A = 20
DIM B = 20
DIM E = 50000
DIM L = 1000
DIM N = 10000
DIM O = 1000
DIM R = 1000
DIM T = 50000




VAR_ALIAS -------------------------------------------------------------------- = A(7)
VAR_ALIAS Session length (min) = A(0)
VAR_ALIAS Mean random interval (sec) = A(1)
VAR_ALIAS ------------------------------------------------------------------- = A(8)






S.S.1,  \Session timer and initialize variables
 S1,
   0": SET A(0)  = 15;        \Session length (min)
       SET A(1)  = 60;        \Mean random interval (sec)
       SET E(0)  = -987.987;
       SET L(0)  = -987.987;  \Times of left presses
       SET N(0)  = -987.987;  \Times of head entries
       SET O(0)  = -987.987;  \Times of pellet delivery
       SET R(0)  = -987.987;  \Times of right presses
       SET T(0)  = -987.987;
       ON ^Fan ---> S2
 S2,
   #START: SET B(0) = A(0) * 1' ---> S3
 S3,
   0.01": ADD H; IF H >= B(0) [@END, @NOEND]
          @END: SET E(G) = 100, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> STOPABORTFLUSH
          @NOEND: ---> S3





S.S.2,  \Houselight
 S1,
   #START: ON ^HouseLight;
           SET E(G) = 8, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> SX





\S.S.3,  \Lever extension, retraction
\ S1,
\   #START: ON ^RightLeverOut, ^LeftLeverOut;
\           SET E(G) = 11, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987;
\           SET E(G) = 13, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> SX





S.S.4,  \Variable interval timer
 S1,
   #START: InitConstProbArr V,A(1); ADD A(9); RANDD I = V; ~J := Round(I);~; SET A(2) = J; SET B(2) = A(2) * 1"; Z1;
           SET E(G) = 15, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> S2
 S2,
   B(2)#T: Z2; SET E(G) = 16, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> S3
 S3,
   #Z3: ADD A(9); RANDD I = V; ~J := Round(I);~; SET A(2) = J; SET B(2) = A(2) * 1"; Z1;
        SET E(G) = 15, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> S2





S.S.5,  \Recording responses
 S1,
   #START: ---> S2
 S2,
   1#R^RightLeverIn: SET R(A(5)) = H/100; ADD A(5);
                     SET R(A(5)) = -987.987;
                     SET E(G) = 1, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> SX
   1#R^LeftLeverIn: SET L(A(3)) = H/100; ADD A(3);
                    SET L(A(3)) = -987.987;
                    SET E(G) = 2, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> SX
   1#R^HeadEntry: SET N(A(4)) = H/100; ADD A(4);
                  SET N(A(4)) = -987.987;
                  SET E(G) = 3, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> SX





S.S.6,  \Pellet delivery
 S1,
   #Z2: ON ^Feeder; SET O(A(6)) = H/100; ADD A(6);
        SET O(A(6)) = -987.987;
        SET E(G) = 10, T(G) = H; ADD G; SET E(G) = -987.987, T(G) = -987.987 ---> S2
 S2,
   0.1": OFF ^Feeder; Z3 ---> S1





S.S.7, \Session timer for display
 S1,
   #START: ---> S2
 S2,
   1": ADD A(7); SHOW 3, SESS TIME(SEC), A(7) ---> S2





S.S.8,  \VI timer for display
 S1,
   #START!#Z1: SET A(8) = A(2) ---> S2
 S2,
   1": SUB A(8); SHOW 4, INTERVAL CNT DOWN, A(8) ---> S2
   #Z2: ---> S1





S.S.9,  \Display
 S1,
   #START: SHOW 1,  INTERVAL#, A(9);
           SHOW 2,  REWARD#, A(6);
           SHOW 6,  RIGHT PRESS, A(5);
           SHOW 7,  LEFT PRESS, A(3);
           SHOW 8,  HEAD ENTS, A(4) ---> S2
 S2,
   0.2": SHOW 1,  INTERVAL#, A(9);
         SHOW 2,  REWARD#, A(6);
         SHOW 6,  RIGHT PRESS, A(5);
         SHOW 7,  LEFT PRESS, A(3);
         SHOW 8,  HEAD ENTS, A(4) ---> S2
