\  FR1 light+tone.MPC

\  MODIFIED FOR Seattle Children's - 3/28/12
\  FIXED RATIO DRUG SELF INFUSION PROCEDURE
\  THIS IS NOW FOR SA WITH COCAINE - 2 hour session and 20 sec timeout (Fixed Interval 20s)
\  This is the program for 5 sec LIGHT over right lever and TONE (4500 Hz)

\  Many variations on this basic procedure are possible.
\  Only the right lever has been made active although data is collected on both levers.
\  Critical parameters may be set before issuing a START command
\  with K-pulses (defaults given below and shown on the screen).
\  A 10 cc syringe is used in this procedure.
\  Data is automatically copied to disk (MED-PCIV\Data) at end - STOPABORTFLUSH
\  One infusion is delivered at beginning of session.
\
\  3-2012 - Ng-Evans:
\         - Added Named Variables for adjusting parameters, easier than 
\           K-pulse scheme and allows changing after session start
\              - Added A(5) so that session time limit can be changed on the fly
\         - Fixed numerous errors in state sets (timing problems, missing colons etc.)
\         - Adjusted spacing throughout to increase program readibility
\         - Removed hard-coded inputs and outputs and used constants instead (shortened names of constants)
\ 




\   OUTPUTS
\
   ^leftlever  = 1
   ^rightlever = 2
   ^feeder     = 3
   ^leftlt     = 4
   ^rightlt    = 5
   ^traylt     = 6
   ^houselt    = 7
   ^tone       = 8
   ^pump       = 9
   ^fan        = 10


\
\   INPUTS
\
   ^leftleverin  = 1
   ^rightleverin = 3
   ^headentry    = 5



\  VARIABLES USED IN THIS PROGRAM

\   A ARRAY =  Counters for program control
\   A(0) = Fixed Ratio Setting (Default = 1)
\   A(1) = Session Time in minutes (Default = 120 minutes)
\   A(2) = Maximum number of injections (Default =  200)
\   A(3) = Timeout following injection (Default = 20 seconds)
\   A(4) = Line Indicator during set up
\   A(5) = Used in session time countdown so you can change A(1) on the fly

\   B ARRAY =  Counters for data elements
\   B(0) = LeftLever Responses
\   B(1) = RightLever Responses
\   B(2) = Number of Infusions
\   B(3) = Time in Fixed Ratio in seconds
\   B(4) = Lever Responses/minute (@ Fixed Ratio)

\   C ARRAY =  IRT Array and event pen code for soft cum. recorder

\   I  =  Subscript for the IRT Array C
\   T  =  Clock Ticks for IRT's.  Resolution = 0.1 second.

\   THE FOLLOWING K-PULSE SCHEME MAY BE AVOIDED BY CLICKING DELTA-X ICON AND CHOOSING 'NAMED VARIABLES'.
\   WITH NAMED VARIABLES, THE ITEMS ARE IN A MENU WITH NAMES AND CAN BE CHANGED TO WHATEVER IS 
\   DESIRED IN ONE STEP.
\   K-PULSE SCHEME USED IN THIS PROGRAM TO SET PROG. CONTROL CTRS.
\   K1   Adds 1 to the default value
\   K2   Subtracts 1 from the default value
\   K5   Adds 5 to the default value
\   K6   Subtracts 5 from the default value
\   K10  Adds 10 to the default value
\   K11  Subtracts 10 from the default value
\   K15  Cycles among various counters as indicated by LINE#
\        where line 6 sets up the fixed ratio counter, A(0),
\        line 11 sets up the session time, A(1), etc.
\   Issue #START command to exit the setup procedure and begin
\   a test session.

\   Z-PULSES USED IN THIS PROGRAM

\   Z1 =  Ratio State Set activated
\   Z2 =  Reinforcement State Set activated
\   Z3 =  Timeout State activated
\   Z4 =  Reset Pulse for IRT Timer/Counter
\   Z5 =  End of Session Pulse



VAR_ALIAS Fixed Ratio = A(0)
VAR_ALIAS Session Time (min) = A(1)
VAR_ALIAS Max number infusions = A(2)
VAR_ALIAS Time-out duration (sec) = A(3)


\   Dimension All Arrays

LIST A = 1,120,100,20,6,0
DIM B  = 10
DIM C  = 10000


\   *********************************************
\        DISPLAY DEFAULT SET UP VALUES
\          CHANGES MADE VIA K-PULSES
\   *********************************************

S.S.1,
S1,
   0.01": SHOW 1, Line#, A(4), 6, Ratio, A(0), 11, SessTime, A(1), 16, MaxInj, A(2);
          SHOW 21, TimOut, A(3) ---> S2

S2,  \ SET Line #6, FIXED RATIO
   #K15:SET A(4) = 11--->S9
   #K1:SET A(0) = A(0)+1--->S9
   #K2:SET A(0) = A(0)-1--->S9
   #K5:SET A(0) = A(0)+5--->S9
   #K6:SET A(0) = A(0)-5--->S9
   #K10:SET A(0) = A(0)+10--->S9
   #K11:SET A(0) = A(0)-10--->S9
   #START: ---> S10

S3,  \ SET Line #11, SESSION TIME
   #K15:SET A(4) = 16--->S9
   #K1:SET A(1) = A(1)+1--->S9
   #K2:SET A(1) = A(1)-1--->S9
   #K5:SET A(1) = A(1)+5--->S9
   #K6:SET A(1) = A(1)-5--->S9
   #K10:SET A(1) = A(1)+10--->S9
   #K11:SET A(1) = A(1)-10--->S9
   #START: ---> S10

S4,  \ SET Line #16, MAXIMUM # OF INJECTIONS
   #K15:SET A(4) = 21--->S9
   #K1:SET A(2) = A(2)+1--->S9
   #K2:SET A(2) = A(2)-1--->S9
   #K5:SET A(2) = A(2)+5--->S9
   #K6:SET A(2) = A(2)-5--->S9
   #K10:SET A(2) = A(2)+10--->S9
   #K11:SET A(2) = A(2)-10--->S9
   #START: ---> S10

S5,  \ SET Line #21, TIMEOUT INTERVAL
   #K15:SET A(4) = 9--->S9
   #K1:SET A(3) = A(3)+1--->S9
   #K2:SET A(3) = A(3)-1--->S9
   #K5:SET A(3) = A(3)+5--->S9
   #K6:SET A(3) = A(3)-5--->S9
   #K10:SET A(3) = A(3)+10--->S9
   #K11:SET A(3) = A(3)-10--->S9
   #START: ---> S10
S9,
   0.01":SHOW 1, Line#, A(4), 9, Ratio, A(0), 11, SessTime, A(1), 16, MaxInj, A(2);
         SHOW 21, TimOut, A(3);
         IF A(4) = 11 [@T,@F]
            @T:--->S3
            @F:IF A(4) = 16 [@T,@F]
                  @T:--->S4
                  @F:IF A(4) = 21 [@T,@F]
                        @T:--->S5
                        @F:--->S2
S10,  \ Once a session is started prohibit changing
      \ control counters with K-Pulses.

    10':--->SX




\   *********************************************
\              MAIN PROGRAM FOR FIXED RATIO
\   *********************************************

S.S.2,
S1,
   #START: ON ^leftlever, ^rightlever, ^houselt, ^pump ---> S2  \includes one initial infusion to load catheter line
S2,
   1": OFF ^pump ---> S3
S3,
   A(0)#R^rightleverin: ON ^rightlt, ^houselt, ^pump, ^tone; Z2 ---> S4
   #Z5: OFF ^leftlever, ^rightlever, ^houselt ---> S7     \ End Session
S4,
   1": OFF ^pump ---> S5
S5,
   4": OFF ^rightlt, ^tone; Z3 ---> S6 \This will give light+tone and infuse 0.2mg cocaine in 0.05 ml
S6,
   A(3)*100#T: Z1 ---> S3 \allows TO period
S7,
   5': ---> SX    \ Holding State While Data is cleared





\   *********************************************
\   DATA COLLECTION (DISPLAY/COUNTERS/PRINT/ETC.)
\   *********************************************

S.S.3,  \  RightLever Responses
S1,
   #START: ---> S2
S2,      \  Count Right Lever Responses
   #R^rightleverin: ADD B(1) ---> SX
   #Z5: ---> S3
S3,
   5': ---> SX    \ Holding State While Data is cleared




S.S.4,  \  LeftLever Responses
S1,
   #START: ---> S2
S2,      \  Count Left Lever Responses
   #R^leftleverin: ADD B(0) --->SX
   #Z5: ---> S3
S3,
   5': ---> SX    \ Holding State While Data is cleared




S.S.5,   \  Count Reinforcements and end session if max reached
S1,
   #START: ---> S2
S2,
   #Z2: ADD B(2); IF B(2) = A(2) [@MaxInject,@Cont]
           @MaxInject: SET B(4) = B(1)/B(3)*60; PRINT --->S3
           @Cont: ---> SX
S3,
   30": ---> STOPABORTFLUSH




S.S.6,   \  Update screen display
S1,
   #START: ---> S2
S2,
   0.1": SHOW 6, Ratio, A(0), 16, MaxInf, A(2), 21, TimeOut, A(3);
         SHOW 1, LL#resp, B(0);
         SHOW 2, RR#resp, B(1);
         SHOW 3, Infusions, B(2);
         SHOW 4, FRTime(sec), B(3);
         SHOW 11, SessTime, A(1) ---> SX




\ ***********************************************
\    INCREMENT TIME T FOR IRT AND EVENT ARRAY
\ ***********************************************

S.S.7,
S1,
   #START: ---> S2
S2,
   #Z4: ---> S2
   0.1": ADD T ---> SX




\ *****************************************************
\      SET RESPONSE IRT'S AND EVENTS IN ARRAY C
\ *****************************************************

S.S.8,  \  SET RESPONSE IRT VALUES
S1,
   #START: SET C(I) = -987.987 ---> S2
S2,
   #R^rightleverin: SET C(I) = T+0.1, T = 0; Z4;
                    IF I = 10000 [@EndSession,@Cont]
                    @End: Z5 ---> SX
                    @Cont: ADD I; SET C(I) = -987.987 ---> SX

   #R^leftleverin: SET C(I) = T+0.50, T = 0; Z4;
                   IF I = 9999 [@EndSession,@Cont]
                   @End: Z5 ---> SX
                   @Cont: ADD I; SET C(I) = 0.60;
                          ADD I; SET C(I) = -987.987 ---> SX




S.S.9,   \ SET REINFORCEMENT MARK FOR SOFT CUM. RECORDER
S1,
   #START: ---> S2
S2,
   #Z2: SET C(I) = 0.2;
        IF I = 10000 [@EndSession,@Cont]
        @End: Z5 ---> SX
        @Cont: ADD I; SET C(I) = -987.987 ---> SX




\   *********************************************
\        INCREMENT TIME IN FIXED RATIO
\   *********************************************

S.S.10,
S1,
   #Z1: ---> S2
S2,
   1": ADD B(3) ---> SX
   #Z2: ---> S1
   #Z5: ---> S3
S3,
   5': ---> SX    \ Holding State While Data is cleared




\   *********************************************
\                  SESSION CLOCK
\   *********************************************

S.S.11,
S1,
   #START: --->S2
S2,
   1': ADD A(5); IF A(5) >= A(1) [@EndUnlessInfusionOn,@Continue]
                 @EndUnless: IF S.S.2 > 3 [@Wait,@EndSession]
                             @Wait: ---> S2
                             @End: Z5 ---> S3
                 @Continue: ---> SX
S3,   \ Calculate Response Rate in Ratio
   .01": SET B(4) = (B(1)/B(3))*60 ---> S4
S4,
   10": ---> STOPABORTFLUSH




