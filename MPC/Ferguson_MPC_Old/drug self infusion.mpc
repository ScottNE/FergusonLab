\ Copyright (C) 2008 MED Associates, All rights reserved.

\ Drug Self Infusion.mpc

\ Many variations on this basic procedure are possible.
\ Only the Right Lever has been made active, although data
\ is collected on both Levers.  The House Light is on
\ through out the procedure, the Stimulus Light above
\ the Right Lever is turned on when the procedure enters the
\ Fixed Ratio component and is turned off during the Time out.
\ Critical parameters have been assigned variable values that
\ may be set before issuing a START command.


\ Inputs:
^LeftLever      = 1
^Left Position  = 2
^RightLever     = 3
^Right Position = 4


\ Outputs:
^LeftLeverOut   = 1
^RightLeverOut  = 2
^Feeder         = 3
^LeftStimLight  = 4
^RightStimLight = 5
^WhiteNoise     = 6
^HouseLight     = 7
^Pump           = 8


\ A() = Control Variables with Assigned Aliases as Defined
Var_Alias Fixed Ratio Value                 = A(0)  \ Default = 10
Var_Alias Session Length (min)              = A(1)  \ Default = 30 minutes
Var_Alias Maximum Number of Infusions       = A(2)  \ Default = 10
Var_Alias Time out Following Infusion (sec) = A(3)  \ Default = 60 seconds
Var_Alias Infusion Time (sec)               = A(4)  \ Default = 2 seconds
Var_Alias SoftCR Data Array (Yes=1  No=0)   = A(5)  \ Default = 1-Yes


\ List Data Variables Here
\  B() = Counters for data elements
\  B(0) = RightLever Responses (@ Fixed Ratio)
\  B(1) = RightLever Responses (@ Infusion)
\  B(2) = RightLever Responses (@ Time out)
\  B(3) = LeftLever  Responses (@ Fixed Ratio)
\  B(4) = LeftLever  Responses (@ Infusion)
\  B(5) = LeftLever  Responses (@ Time out)
\  B(6) = Time in Fixed Ratio in Seconds
\  B(7) = Number of Infusions
\  B(8) = Lever Responses/Minute (@ Fixed Ratio)

\  C() = IRT Array and Event Pen Code for Soft Cum. Recorder


\ List Working Variables Here
\  D = Session Count Down Timer
\  F = Time Out in MED Ticks
\  G = Infusion Time in MED Ticks
\  I = Subscript for the IRT Array C
\  T = Elapsed Time in 0.01 sec Increments for SoftCR Data


DIM A = 5
DIM B = 10
DIM C = 10000


\ Z-Pulses Used in this Program
\  Z1  = Ratio State Set Activated
\  Z2  = Reinforcement State Set Activated
\  Z3  = Timeout State Activated
\  Z32 = End of Session Signal


\***************************************************
\            Drug Self Infusion Schedule
\  S1 - Set Default Values
\       Fixed Ratio Value            (10)
\       Session Length               (30 minutes)
\       Maximum Number of Infusions  (10)
\       Time Out Following Infusion  (60 seconds)
\       Infusion Time                (2 seconds)
\       SoftCR Activation            (1-Yes)
\***************************************************
S.S.1,
S1,     \ Set default values for Fixed Ratio, Session Length,
        \ Maximum Number of Infusions, Time Out, and Infusion Time.
  0.001": SET A(0) = 10, A(1) = 30, A(2) = 10, A(3) = 60, A(4) = 2, A(5) = 1 ---> S2

S2,     \ Wait for START command.  Change variable F to seconds,
        \ turn House and Right Stimulus lights on, issue Z1 signal.
        \ Update Display with default or changed variable values.
  #START: CLEAR 1,60;
          SET D = A(1);
          SET F = A(3) * 1";
          SET G = A(4) * 1";
          ON ^HouseLight, ^RightStimLight; Z1 ---> S3
  1": SHOW 1,Fixed Ratio,A(0), 2,Session Length,A(1), 3,Max Infusions,A(2);
      SHOW 4,Time Out,A(3),    5,Infusion Time,A(4),  6,SoftCR Code,A(5) ---> SX

S3,     \ Following a fixed number of responses determined
        \ by the value of A(0) turn the pump on and issue
        \ a Z2 pulse.
  A(0)#R^RightLever: ON ^Pump; Z2 ---> S4
  #Z32: OFF ^RightStimLight ---> S6    \ End Session

S4,     \ Infusion time set by variable A(5).  Turn pump and stimulus OFF.
        \ Issue Z3 signal.  The PHM-100 pump uses a 3.33 RPM motor
        \ and will deliver 36.734 ul with a 10 cc Multifit syringe
        \ and 65.5 ul with a 20 cc Multifit syringe in 2 sec.  See the
        \ motor/syringe table supplied with the pump for specific
        \ flow rates for your syringe and adjust this time value
        \ with any text editor.  Run Trans after making any changes.
        \ Solutions may also be adjusted to get the mg/kg dose per
        \ infusion desired.
  G#T: OFF ^RightStimLight, ^Pump; Z3 ---> S5

S5,     \ Time out following Infusion.  Set by array
        \ element A(3).  Turn Stimulus Light on,
        \ signal start of FR and return to State 3.
  F#T: ON ^RightStimLight; Z1 ---> S3
  #Z32: ---> S6     \ End Session

S6,     \ Holding State at End of Session
  5': ---> SX


\*********************************************
\         COUNT RIGHT LEVER RESPONSES
\*********************************************
S.S.2,
S1,
  #START: --->S2

S2,     \ Count Responses in Ratio
  #R^RightLever: ADD B(0) ---> SX
  #Z2:  ---> S3
  #Z32: ---> S5

S3,     \ Count Responses During Infusion
  #R^RightLever: ADD B(1) ---> SX
  #Z3:  ---> S4
  #Z32: ---> S5

S4,     \ Count Responses During Time Out
  #R^RightLever: ADD B(2) ---> SX
  #Z1:  ---> S2
  #Z32: ---> S5

S5,     \ Holding State at End of Session
  5': ---> SX


\*********************************************
\         COUNT LEFT LEVER RESPONSES
\*********************************************
S.S.3,
S1,
  #START: ---> S2

S2,     \ Count Responses in Ratio
  #R^LeftLever: ADD B(3) ---> SX
  #Z2:  ---> S3
  #Z32: ---> S5

S3,     \ Count Responses During Infusion
  #R^LeftLever: ADD B(4) ---> SX
  #Z3:  ---> S4
  #Z32: ---> S5

S4,     \ Count Responses During Time Out
  #R^LeftLever: ADD B(5) ---> SX
  #Z1:  ---> S2
  #Z32: ---> S5

S5,     \ Holding State at End of Session
  5': ---> SX


\*********************************************
\               COUNT INFUSIONS
\*********************************************
S.S.4,
S1,
  #START: ---> S2

S2,     \ Count Infusions and End Session if the
        \ maximum count determined by A(2) is reached.
  #Z2: ADD B(7); IF B(7) >= A(2) [@MaxInject, @Cont]
                    @Max:  ---> S3
                    @Cont: ---> SX

S3,     \ Wait for the last Time Out period to finish
        \ and Signal the End of Session.
  #Z1: Z32 ---> S1


\*********************************************
\   UPDATE SCREEN DISPLAY WITH ALL COUNTERS
\*********************************************
S.S.5,
S1,
  #START: ---> S2

S2,
  0.1": SHOW 1,Rgt Lever Resp in FR,B(0), 2,Rgt Lever Resp in Infusion,B(1), 3,Rgt Lever Resp in TO,B(2);
        SHOW 6,Lft Lever Resp in FR,B(3), 7,Lft Lever Resp in Infusion,B(4), 8,Lft Lever Resp in TO,B(5);
        SHOW 11,FR Time,B(6),             12,Remaining Session Time,D,       13,# of Infusions,B(7) ---> SX


\**********************************************
\   INCREMENT TIME T FOR IRT AND EVENT ARRAY
\**********************************************
S.S.6,
S1,
  #START: ---> S2

S2,     \ Increment T every 10 ms
  0.01": ADD T ---> SX
  #Z32: ---> S1


\*********************************************************
\ ADD A STEP ELEMENT TO ARRAY C FOR EACH CORRECT RESPONSE
\*********************************************************
S.S.7,
S1,
  #START: IF A(5) = 1 [] ---> S2

S2,     \ For each response on the Right Lever set the
        \ element I of Array C to the value of T plus
        \ a (.10) Response Code for SoftCR.  Zero T.  Test
        \ if Array is full and "Seal" with -987.987 code.
  #R^RightLever: SET C(I) = T + 0.10, T = 0;
                 IF I >= 10000 [@ArrayFull, @Cont]
                    @Full: ---> S1
                    @Cont: ADD I; SET C(I) = -987.987 ---> SX
  #Z32: ---> S1


\*************************************************************
\ ADD AN EVENT ELEMENT TO ARRAY C FOR EACH INCORRECT RESPONSE
\*************************************************************
S.S.8,
S1,
  #START: IF A(5) = 1 [SET C(I) = 0.50, I = I + 1, C(I) = -987.987] ---> S2

S2,     \ For each Response on the Left Lever, set a
        \ up Pen Code for Event 0 (.60) and then Reset
        \ the Pen (.50) to create a "Pip" on the Event
        \ Line.  Finally test and Seal the array.
  #R^LeftLever: SET C(I) = T + 0.60, T = 0;
                IF I >= 10000 [@ArrayFull, @Cont]
                   @Full: ---> S1
                   @Cont: ADD I; SET C(I) = 0.50;
                          ADD I; SET C(I) = -987.987 ---> SX
  #Z32: ---> S1


\****************************************************
\    ADD A PIP ELEMENT TO ARRAY C FOR EACH REWARD
\****************************************************
S.S.9,
S1,
  #START: IF A(5) = 1 [] ---> S2

S2,
  #Z2: SET C(I) = T + 0.20, T = 0;
       IF I >= 10000 [@ArrayFull, @Cont]
          @Full: ---> S1
          @Cont: ADD I; SET C(I) = -987.987 ---> SX


\*********************************************
\        INCREMENT TIME IN FIXED RATIO
\*********************************************
S.S.10,
S1,     \ Wait for Z1 signalling start of FR
  #Z1: ---> S2

S2,     \ Increment B(6) every 1 second until
        \ End of FR (infusion) is signaled by Z2
        \ or End of Session signaled by Z32.
  1": ADD B(6) ---> SX
  #Z2:  ---> S1
  #Z32: ---> S3

S3,     \ Holding State at End of Session
  5': ---> SX


\*********************************************
\                SESSION CLOCK
\*********************************************
S.S.11,
S1,
  #START: ---> S2

S2,     \ Count down Session Time in minutes.
        \ End immediately unless pump is running
        \ (S.S.1, S4) then advance to State 3.
  1': SUB D; IF D <= 0 [@EndUnlessInfusionOn, @Continue]
                @EndUnless: IF S.S.1 = 4 [@Wait, @EndSession]
                               @Wait: ---> S3
                               @End: Z32 ---> S4
                @Continue: ---> SX
  #Z32: ---> S4

S3,     \ Wait for End of last Infusion and Time Out periods.
        \ Issue End of Session Signal, Z32.
  #Z1: Z32 ---> S4

S4,     \ Calculate Responses/Minute in FR.
        \ Set a Pen Reset Code (.31) in the SoftCR array, if enabled.
  0.01": SET B(8) = B(0) / (B(6) / 60);
         SHOW 15,Resp/Min,B(8);
         IF A(5) > 0 [@SoftCR, @Not]
            @SoftCR: SET C(I) = T + 0.31, I = I + 1, C(I) = -987.987 ---> S5
            @Not: SET C(0) = -987.987 ---> S5

S5,     \ Wait for Screen Update and end with
        \ STOPABORTFLUSH for Automatic Data Saving
  5": OFF ^HouseLight ---> STOPABORTFLUSH

