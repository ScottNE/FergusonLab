\
\ ************************
\ *                      *
\ *   SF_COKED_FR or PR  *
\ *                      *
\ ************************ 
\ Scott Ng-Evans (2013)
\ Based on RP_COKED_FR (2009)
\ Methods modified from: 
\           Prefrontal synaptic markers of cocaine addiction-like behavior
\           in rats. F Kasanetz, M Lafourcade, V Deroche-Gamonet, J-M Revest, 
\           N Berson, E Balado, J-F Fiancette, P Renault, P-V Piazza and OJ Manzoni.
\           Molecular Psychiatry (2012), 1-9.
\ 
\
\                 Basic training protocol: The daily SA session was composed 
\                 of three drug components (40 min each) separated by 15 min 
\                 drug free periods. "Drug" periods were signaled by the blue 
\                 cue light, while the "no-drug" periods were signaled by 
\                 illumination of the entire SA box and extinction of the blue 
\                 cue light. During the "no-drug" periods, nose pokes were without 
\                 scheduled consequences. During the "drug" periods, introduction 
\                 of the animal's nose into one hole (active device) turned on the 
\                 white cue light located above it and then, 1 sec later, switched 
\                 on the infusion pump. The cue light remained on for a total of 4 
\                 sec. Nose pokes into the other hole (inactive device) had no 
\                 scheduled consequences. The self-infusion volume was 0.5 ?L (2 sec 
\                 infusion) and contained 2 ug/g of cocaine. Each infusion was followed 
\                 by a 40 sec time-out period. During the first 5 days, an FR1 schedule 
\                 of reinforcement (i.e. one nose poke resulted in an infusion of 2 ug/g 
\                 of cocaine) was applied. Then, the FR was first increased to 3 (1 to 2 
\                 sessions) and finally to 5 for the rest of the experiment. Criterion 
\                 for acquisition of cocaine SA was defined by a stable number of 
\                 self-infusions over at least three consecutive SA sessions (+/-10%). 
\
\
\ MODIFIED FROM SF_COKED_FR
\ Methods from Kasanetz paper above:
\                 
\                Progressive-ratio schedule: The ratio of responses per infusion was 
\                increased after each infusion according to the following progression 
\                (10, 20, 30, 45, 65, 85, 115, 145, 185, 225, 275, 325, 385, 445, 515, 585, 
\                665, 745, 835, 925, 1025, 1125, 1235, 1345, 1465, 1585), the last ratio 
\                completed, called the breakpoint, was used to measure motivation for cocaine. 
\                The session ceased after either 5 hours or when a period of 1 hour had 
\                elapsed since the previously earned infusion.
\
\

\
\
\ Revisions:
\
\
\           7 - 2009 - Added head entry detection as requested by Ben Whiddon
\
\           9 - 2013 - Kerry Kerstetter: "The program should have 3 â€“ 40minute 
\                      periods of drug self administration and each of these periods 
\                      should be followed by a 15minute period with no drug available. 
\                      Total session time would be 165minutes. However when running 
\                      the program we noticed that the session was ending early 
\                      because it was missing the last drug free 15minute period. 
\                      Could you double check/edit the program for us to include 
\                      the last 15m drug free period?...
\                      For ease of use,  could we also have a data display for 
\                      total session time? Currently, the program displays number 
\                      of Active Left Lever Responses and Receptacle Head Entries, 
\                      For ease of use,  could we also have a data display for total 
\                      session time? Currently, the program displays number of Active 
\                      Left Lever Responses and Receptacle Head Entries, neither of 
\                      which will be necessary for the experiment. Could you remove these?"
\
\          6 - 2014 - Lindsay Yager: "Self-Administration on an FR schedule: Rats will be 
\                     trained to lever press for I.V. infusions of cocaine 
\                     (0.75 mg/kg/infusion, 50ul delivered over 2.8 sec). 
\                     The beginning of the session will be indicated by insertion 
\                     of both levers into the chamber. Each cocaine infusion 
\                     will coincide with illumination of a the cue light above the lever 
\                     for 20 sec during which time responses will not result in additional
\                     cocaine delivery (infusion should start when cue light turns on). 
\                     Inactive responses will be without consequence. For the first 2 
\                     sessions cocaine will be available on a FR1 schedule. For sessions 3-4, 
\                     cocaine will be available on a FR3 schedule. Following session 4, cocaine 
\                     will be available during each self-administration session on a FR5 schedule. 
\                     These self-administration sessions will be terminated after a maximum of 20 
\                     infusions or after 3 hr, whichever occurs first. The end of the session will 
\                     be indicated by retraction of the levers. No houselight will be illuminated 
\                     during these sessions. Thus, I will either need 1 program where I can change 
\                     the variable for the FR or three programs with FR1, FR3, and FR5. I won't need 
\                     one of your extraction programs for this data- just displaying the number of
\                     infusions earned, active lever presses, inactive lever presses, and time to 
\                     session completion on the screen will be enough."
\         12 - 2014 - Lindsay Yager: "Would you be able to modify the existing program so that the 
\                     levers are retracted during the 25 minute no drug available periods and the 
\                     houselight remains off the entire session?"
\
\
\          1 - 2016 - Change defaults based on AFG spiking.
\                     Changed FR PR state set to improve it.
\
\          
\          8 - 2016 - Tim O'Neal: "Each self-administration session will last for a total of 6 hours, 
\                     which will consist of 12 blocks of 5-minute trials of drug access followed by a 
\                     25-minute timeout periods. During each trial, depressing the active lever will 
\                     result in illumination of the LED above the lever and activation of the infusion 
\                     pump for 1 second each, with no time-out between infusions (i.e., the active lever 
\                     may be pressed continuously for repeated infusions on an FR1 schedule of reinforcement). 
\                     During each timeout period, the house light will illuminate and active lever pressing 
\                     will not result in LED illumination or drug infusion. Throughout the entire session, 
\                     inactive lever pressing will not result in LED illumination or drug infusion."
\                   - This becomes "TOM_SF_COKED..." , 
\                     In the statement "will consist of 12 blocks of 5-minute trials of drug access followed by a 
\                     25-minute timeout periods.", the 25-minute periods are not "timeout periods", but rather
\                     no-drug periods or no drug-access periods. There is no timeout period after infusions.
\
\
\
\
\        6 - 2025 - C. Burke - Needs refesh -
\                            - 'right' and 'left' references changed to active and inactive 
\                               appropriately in event stamps and cumulative variables
\                            - Simplified display outputs
\                            - New variable for cue light on duration
\                            - Use of PumpOff output signal for dial pumps with pump off
\                              controller circuitry
\                            - Added option for extinction in infusion duration, 0:extinction
\
\                - This is CB_FR_COKED_SPIKING_Left and CB_FR_COKED_SPIKING_Right (PR for PR versions)






\
\
\ Input Constants

^LeftLeverIn  = 1
^RightLeverIn = 3
^HeadEntry    = 5


\ Output Constants (NOTE: Lever refers to extending or retracting)

^LeftLeverOut   = 1
^RightLeverOut  = 2
^Feeder         = 3
^LeftCueLight   = 4
^RightCueLight  = 5
^HouseLight     = 7
^SyringePumpOn  = 9
^Fan            = 10
^SyringePumpOff = 14



\ Z-Pulses
\
\ Z1 time-out over, drug available and drug period starts
\ Z2 drug delivery initiation
\ Z3 no-drug period starts



\ Variable Definitions:

\ A(0)  = Active lever (0:right 1:left)
\ A(1)  = Number of drug periods
\ A(2)  = Length of drug periods (min)
\ A(3)  = Length of no-drug periods (min)
\ A(4)  = Drug infusion time (sec)
\ A(5)  = Post-infusion time-out length (sec)
\ A(6)  = Tally for number of drug periods
\ A(7)  = Switch for ending session when number of drug periods reached
\ A(8)  = Inter-infusion time limit; max time allowed between infusions before PR ends  **** NOT USED IN FR ***
\ A(9)  = Inter-infusion timer (1-second resolution)   **** NOT USED IN FR ****
\ A(10) = Number of drug period active presses
\ A(11) = Number of no-drug period active presses
\ A(12) = Number of drug period inactive presses
\ A(13) = Number of no-drug period inactive presses
\ A(14) = Variable for countdown of drug and no-drug period to display
\ A(15) = Max session length in hours
\ A(16) = Food tray head entries
\ A(17) = Break point time-limit in minutes, when enough time elapses 
\          since last response to end session and record break point
\ A(18) = Initial value of X because X changes
\ A(19) = Last ratio for break point
\ A(20) = Breakpoint; zero before break point reached
\ A(25) = Session time displayed in minutes
\ A(26) = Dummy variable for Wizard first-line bug
\ A(27) = Dummy variabe for menu display
\ A(28) = Maximum number of drug infusions to end session
\ A(29) = Houselight during the drug periods (0:off 1:on)
\ A(30) = Houselight for no-drug periods (0:off 1:on)
\ A(31) = Levers retract for no-drug periods (0:no 1:yes)
\ A(32) = Cue light on time after infusion initiation (sec)
\ A(33) = A(2), length of drug period (min), in system time
\ A(34) = A(3), length of no-drug periods (min), in system time
\ A(35) = A(4), Drug infusion time (sec), in system time
\ A(36) = A(5), Post-infusion time-out length (sec), in system time
\ A(37) = A(17), Break point time-limit (min), in system time
\ A(38) = A(32), Cue light on time during infusion (sec), in system time
\ A(39) = A(15), maximum session length (hours), in system time
\
\ B = List of progressive ratios
\
\ C(0) = Subscript for advancing in B list
\
\ D = event time stamp
\ E = event identity stamp
\       3  - Active lever press
\       4  - Inactive lever press
\       5  - Infusion start
\       6  - Infusion end
\       7  - Food tray head entry
\      10  - Active lever extends
\      11  - Inactive lever extends
\      12  - Active lever retracts
\      13  - Inactive lever retracts
\      38  - Active lever cue light on
\      39  - Inactive lever cue light on
\      40  - Active lever cue light off
\      41  - Inactive lever cue light off
\      42  - Houselight on
\      43  - Houselight off
\      44  - Drug period starts
\      45  - No-drug period starts
\      46  - Missed infusion during extinction
\     100  - End session marker
\      
\ F = FR countdown variable
\ G = Event counter
\ H = Session timer
\
\ X = fixed ratio
\ Y(0) = Number of infusions
\ Y(1) = Number of missed infusions (during extinction)


DIM A = 50
DIM C = 10
DIM D = 50000
DIM E = 50000
DIM Y = 1

LIST B = 5, 10, 20, 30, 45, 65, 85, 115, 145, 185, 225, 275, 325, 385, 445, 515, 585, 665, 745, 835, 925, 1025, 1125, 1235, 1345, 1465, 1585



VAR_ALIAS -------------------------------------------------------------------- = A(26)
VAR_ALIAS 01)Max session length(hour) = A(15)
VAR_ALIAS 02)Max number of drug infusions = A(28)
VAR_ALIAS 03)Fixed Ratio or PR (PR:-1) = X
VAR_ALIAS 04)PR break pnt time-limit(min) = A(17)
VAR_ALIAS 05)Active lever (0:right 1:left) = A(0)
VAR_ALIAS 06)Levers retract for no-drug periods (0:no 1:yes) = A(31)
VAR_ALIAS 07)Houselight for drug periods (0:off 1:on) = A(29)
VAR_ALIAS 08)Houselight for no-drug periods (0:off 1:on) = A(30)
VAR_ALIAS 09)Number of drug periods(minimum 1) = A(1)
VAR_ALIAS 10)Length of drug periods(min) = A(2)
VAR_ALIAS 11)Length of no-drug periods(min) = A(3)
VAR_ALIAS 12)Drug infusion time(sec) 0:extinction = A(4)
VAR_ALIAS 13)Cue lt on time (sec) = A(32)
VAR_ALIAS 14)Post-inf time-out length(sec) = A(5)
VAR_ALIAS ------------------------------------------------------------------- = A(27)








S.S.1,  \ Default set-up, drug/no-drug switching
  S1,  
      0": SET A(0)  = 1;       \ Active lever (0:right 1:left)
          SET A(1)  = 12;      \ Number of drug periods (minimum 1)
          SET A(2)  = 5;       \ Length of drug periods (min)
          SET A(3)  = 25;      \ Length of no-drug periods (min)
          SET A(4)  = 2.8;     \ Drug infusion time (sec) - 0:extinction
          SET A(5)  = 0;       \ Post-infusion time-out length (sec)
          SET A(15) = 6;       \ Maximum session length (hours)
          SET A(17) = 60;      \ Break point time-limit in minutes, when enough time elapses since last response to end session and record break point
          SET A(28) = 5000;    \ Maximum number of drug infusions
          SET A(29) = 0;       \ Houselight during the drug periods (0:off 1:on)
          SET A(30) = 1;       \ Houselight for no-drug periods (0:off 1:on)
          SET A(31) = 0;       \ Retract levers during no-drug periods (0:no 1:yes)
          SET A(32) = 1;       \ Cue lt on time after infusion initiation(sec)
          SET D(0) = -987.987, E(0) = -987.987; \ Time and date stamp arrays
          SET X = 1;           \ Fixed ratio or PR (PR:-1)
          
          \ Time conversion variables
          SET A(33) = A(2) * 1';       \ length of drug period (min), in system time
          SET A(34) = A(3) * 1';       \ length of no-drug periods (min), in system time
          SET A(35) = A(4) * 1";       \ Drug infusion time (sec), in system time
          SET A(36) = A(5) * 1";       \ Post-infusion time-out length (sec), in system time
          SET A(37) = A(17) * 1';      \ Break point time-limit (min), in system time
          SET A(38) = A(32) * 1";      \ Cue light on time after infusion initiation (sec), in system time
          SET A(39) = (A(15)*60) * 1'; \ Maximum session length (hours), in system time
          ON ^Fan ---> S2
  S2, 
      #START: SET D(G) = H; SET E(G) = 44; ADD G; SET D(G) = -987.987, E(G) = -987.987;
              SET A(18) = X, A(33) = A(2) * 1'; 
              Z1; 
              ADD A(6) ---> S3
  S3,
      A(33)#T: IF S.S.4 = 1 [@SWITCH, @WAIT]
               @SWITCH: IF A(6) >= A(1) [@END, @NOEND]
                        @END:  SET A(7) = 1 ---> S6
                        @NOEND: Z3; 
                                SET D(G) = H, E(G) = 45; ADD G; SET D(G) = -987.987, E(G) = -987.987;
                                SET A(34) = A(3) * 1' ---> S4
               @WAIT: ---> S5 \wait for post-infusion timeout to complete
  S4,
      A(34)#T: Z1; 
               ADD A(6); 
               SET D(G) = H, E(G) = 44; ADD G; SET D(G) = -987.987, E(G) = -987.987 ---> S3
  S5,  
     .01": IF S.S.4 = 1 [@SWITCH, @WAIT]
           @SWITCH: IF A(6) >= A(1) [@END, @NOEND]
                    @END:  SET A(7) = 1 ---> S6
                    @NOEND: Z3; SET D(G) = H, E(G) = 45; ADD G; SET D(G) = -987.987, E(G) = -987.987 ---> S4
           @WAIT: ---> S5
  S6,
     #START: ---> SX





S.S.2,  \ Fixed and Progressive Ratio
  S1,  
    #Z1: IF A(18) = -1 [@PR, @FR]
         @PR: SET A(19) = X; LIST X = B(C(0)); SET U = X, F = X; 
              SET A(37) = A(17) * 1';
              IF A(0) = 0 [@RIGHT, @LEFT]
              @RIGHT: ---> S3
              @LEFT:  ---> S2
         @FR: SET U = X, F = X; 
              IF A(0) = 0 [@RIGHT, @LEFT]
              @RIGHT: ---> S6
              @LEFT:  ---> S5
  S2,
    1#R^LeftLeverIn: SUB U; 
                     IF U <= 0 [@FRDONE, @FRNOTDONE]
                     @FRDONE: Z2 ---> S1
                     @FRNOTDONE: ---> S2
    A(37)#T: SET A(20) = A(19), A(7) = 1;
             SHOW 7, PR BREAK PNT: , A(20) ---> S4
    #Z3: ---> S1
  S3,
    1#R^RightLeverIn: SUB F; 
                      IF F <= 0 [@FRDONE, @FRNOTDONE]
                      @FRDONE: Z2 ---> S1
                      @FRNOTDONE: ---> S3
    A(37)#T: SET A(20) = A(19), A(7) = 1;
             SHOW 7, PR BREAK PNT: , A(20) ---> S4
    #Z3: ---> S1
  S4,
    1": ---> SX
  S5,
    1#R^LeftLeverIn: SUB U; 
                     IF U <= 0 [@FRDONE, @FRNOTDONE]
                     @FRDONE: Z2 ---> S1
                     @FRNOTDONE: ---> S5
    #Z3: ---> S1
  S6,
    1#R^RightLeverIn: SUB F; 
                      IF F <= 0 [@FRDONE, @FRNOTDONE]
                      @FRDONE: Z2 ---> S1
                      @FRNOTDONE: ---> S6
    #Z3: ---> S1





S.S.3,  \ All drug and no-drug period presses recorded and cumulated
  S1,
    #START: ---> S2
  S2,
    1#R^RightLeverIn: IF A(0) = 0 [@RIGHT, @LEFT]
                      @RIGHT: SET D(G) = H, E(G) = 3; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                              ADD A(10);---> SX
                      @LEFT: SET D(G) = H, E(G) = 4; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                             ADD A(12);
    1#R^LeftLeverIn: IF A(0) = 0 [@RIGHT, @LEFT]
                     @RIGHT: SET D(G) = H, E(G) = 4; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                             ADD A(12);---> SX
                     @LEFT: SET D(G) = H, E(G) = 3; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                            ADD A(10);
    #Z3: ---> S3
  S3,
    1#R^RightLeverIn: IF A(0) = 0 [@RIGHT, @LEFT]
                      @RIGHT: SET D(G) = H, E(G) = 3; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                              ADD A(11);
                      @LEFT: SET D(G) = H, E(G) = 4; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                             ADD A(13);---> SX
    1#R^LeftLeverIn: IF A(0) = 0 [@RIGHT, @LEFT]
                     @RIGHT: SET D(G) = H, E(G) = 4; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                             ADD A(13);
                     @LEFT: SET D(G) = H, E(G) = 3; ADD G; SET D(G) = -987.987; SET E(G) = -987.987; 
                            ADD A(11);---> SX
    #Z1: ---> S2
    

 


S.S.4,  \ Infusion - extinction(all timing and signals are the same but no infusion)
  S1,
    #Z2: IF A(4) = 0 [@EXTINCTION, @NOEXTINCTION]
         @EXTINCTION: SET D(G) = H, E(G) = 46; ADD G; SET D(G) = -987.987; SET E(G) = -987.987;
                      ADD Y(1) ---> S6
         @NOEXTINCTION: ON ^SyringepumpOn; 
                        SET D(G) = H, E(G) = 5; ADD G; SET D(G) = -987.987; SET E(G) = -987.987;
                        ADD Y(0) ---> S2
  S2,
    A(35)#T: OFF ^SyringepumpOn; ON ^SyringePumpOff;
             SET D(G) = H, E(G) = 6; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
  S3,
    0.3": OFF ^SyringePumpOff;
          IF A(5) = 0 [@NOTO, @TO]
          @NOTO: ---> S5
          @TO: ---> S4
  S4,
   ((A(36)-A(35))#T: Z1 ---> S1
  S5,
    0": Z1 ---> S1   \1 TICK BEFORE Z1
  S6,
    A(35)#T: ---> S7
  S7,
    0.3": IF A(5) = 0 [@NOTO, @TO]
          @NOTO: ---> S9
          @TO: ---> S8
  S8,
   ((A(36)-A(35))#T: Z1 ---> S1
  S9,
    0": Z1 ---> S1   \1 TICK BEFORE Z1






S.S.5,  \ Head entry detector
  S1,
    #START: ---> S2
  S2,
    1#R^HeadEntry: SET D(G) = H, E(G) = 7; ADD G; SET D(G) = -987.987; SET E(G) = -987.987;
                   ADD A(16) ---> SX





S.S.6,  \ Houselight
  S1,
    #Z1: IF A(29) = 0 [@DRUGHLOFF, @DRUGHLON]
	     @DRUGHLOFF: OFF ^HouseLight; SET D(G) = H, E(G) = 43; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> SX
	     @DRUGHLON: ON ^HouseLight; SET D(G) = H, E(G) = 42; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> SX
    #Z3: IF A(30) = 0 [@NODRUGHLOFF, @NODRUGHLON]
	     @NODRUGHLOFF: OFF ^HouseLight; SET D(G) = H, E(G) = 43; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> SX
	     @NODRUGHLON: ON ^HouseLight; SET D(G) = H, E(G) = 42; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> SX





S.S.7,   \ Lever Cue lights
  S1,
    #START: ---> S2
  S2,
    #Z2: IF A(0) = 0 [@RIGHT, @LEFT]
         @RIGHT: ON ^RightCueLight; OFF ^LeftCueLight;
                 SET D(G) = H, E(G) = 38; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
         @LEFT:  ON ^LeftCueLight; OFF ^RightCueLight;
                 SET D(G) = H, E(G) = 39; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S3
  S3,
    A(38)#T: IF A(0) = 0 [@RIGHT, @LEFT]
             @RIGHT: OFF ^RightCueLight; SET D(G) = H, E(G) = 40; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> S2
             @LEFT:  OFF ^LeftCueLight; SET D(G) = H, E(G) = 41; ADD G; SET D(G) = -987.987; SET E(G) = -987.987  ---> S2





S.S.8,   \ Levers extend and retract
  S1,
    #Z1: ON ^RightLeverOut, ^LeftLeverOut;
         SET D(G) = H; SET E(G) = 10; ADD G; SET D(G) = -987.987; SET E(G) = -987.987;
         SET D(G) = H; SET E(G) = 11; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> SX
    #Z3: IF A(31) = 1 [@RETRACT, @NORETRACT]
         @RETRACT: OFF ^RightLeverOut, ^LeftLeverOut;
                   SET D(G) = H; SET E(G) = 12; ADD G; SET D(G) = -987.987; SET E(G) = -987.987;
                   SET D(G) = H; SET E(G) = 13; ADD G; SET D(G) = -987.987; SET E(G) = -987.987 ---> SX
         @NORETRACT: ---> SX





S.S.9,  \ Data display 
  S1,
    #START: ---> S2
  S2,
    .2": SHOW 1, INFUSIONS: , Y(0);
         SHOW 2, MISSED INFUSIONS: , Y(1);
         SHOW 4, DRUG ACTIVE LEV: , A(10); 
         SHOW 5, DRUG INACTIVE LEV: , A(12);
         SHOW 6, FOOD TRAY HEAD ENT: , A(16);
         SHOW 10, NODRUG ACTIVE LEV: , A(11);
         SHOW 11, NODRUG INACTIVE LEV , A(13) ---> S2





S.S.10,  \ Drug and no-drug period countdown display
  S1,
    #START: SET A(14) = A(2)*60; SHOW 7, DRUG PERIOD SEC: , A(14) ---> S2
  S2,
    1": SUB A(14); IF A(14) < 0 [@NOMARK, @MARK]
        @NOMARK: ---> S2
        @MARK: SHOW 7, DRUG PERIOD SEC: , A(14) ---> S2
    #Z3: SET A(14) = A(3)*60; SHOW 7, NO-DRUG PERIOD SEC: , A(14) ---> S3
  S3,
    1": SUB A(14); IF A(14) < 0 [@NOMARK, @MARK]
        @NOMARK: ---> S3
        @MARK: SHOW 7, NO-DRUG PERIOD SEC: , A(14) ---> S3
    #Z1: SET A(14) = A(2)*60; SHOW 7, DRUG PERIOD SEC: , A(14) ---> S2





S.S.11,  \ Session time display
  S1,
    #START: ---> S2
  S2,
    1": ADD A(25); SHOW 14, SESS DURATION(MIN): , A(25)/60 ---> S2





S.S.12,  \ Session timer and termination
  S1,
    #START: ---> S2
  S2,
    0.01": ADD H; 
           IF (A(7) = 0) AND (H <= A(39) AND (Y(0) < A(28)) [@NOEND, @YESEND]
	       @NOEND: ---> S2
           @YESEND: IF S.S.4 = 1 [@END, @WAIT]
                    @END: SET D(G) = H, E(G) = 100; ADD G; SET D(G) = -987.987, E(G) = -987.987;
                          SHOW 1, INFUSIONS: , Y(0);
                          SHOW 2, MISSED INFUSIONS: , Y(1);
                          SHOW 4, DRUG ACTIVE LEV: , A(10); 
                          SHOW 5, DRUG INACTIVE LEV: , A(12);
                          SHOW 6, FOOD TRAY HEAD ENT: , A(16);
                          SHOW 10, NODRUG ACTIVE LEV: , A(11);
                          SHOW 11, NODRUG INACTIVE LEV , A(13) ---> STOPABORTFLUSH
                    @WAIT: ---> S2
